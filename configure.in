# -*- shell-script -*-
#
# Copyright (c) 2004, The Board of Trustees of the University of Illinois.
#                     All rights reserved.
# Copyright (c) 2002-2005 The Trustees of Indiana University.  
#                         All rights reserved.
# 
# This file is part of the OSCAR software package.  For license
# information, see the COPYING file in the top level directory of the
# OSCAR source distribution.
#
# $Id: configure.in,v 1.47 2004/05/19 20:28:29 tfleury Exp $
#
# $HEADER$
#

# Init autoconf

AC_INIT(packages/Makefile.am)
AC_CONFIG_AUX_DIR(dist)
AC_PREREQ(2.52)

# Get the version of OSCAR that we are installing

get_version="sh $srcdir/dist/get-oscar-version.sh $srcdir"
OSCAR_VERSION="`eval $get_version --full`"
OSCAR_MAJOR_VERSION="`eval $get_version --major`"
OSCAR_MINOR_VERSION="`eval $get_version --minor`"
OSCAR_RELEASE_VERSION="`eval $get_version --release`"
OSCAR_ALPHA_VERSION="`eval $get_version --alpha`"
OSCAR_BETA_VERSION="`eval $get_version --beta`"
OSCAR_SVN_VERSION="`eval $get_version --svn`"

AC_SUBST(OSCAR_MAJOR_VERSION)
AC_SUBST(OSCAR_MINOR_VERSION)
AC_SUBST(OSCAR_RELEASE_VERSION)
AC_SUBST(OSCAR_ALPHA_VERSION)
AC_SUBST(OSCAR_BETA_VERSION)
AC_SUBST(OSCAR_SVN_VERSION)
AC_SUBST(OSCAR_VERSION)

echo "Configuring OSCAR version $OSCAR_VERSION"

# Intialize automake

AM_INIT_AUTOMAKE(oscar, $OSCAR_VERSION, 'no')

# Just because we might as well... :-)

AC_CANONICAL_HOST

# Make automake clean emacs ~ files for "make clean"

CLEANFILES="*~ .*~ .\#*"
AC_SUBST(CLEANFILES)

# Look for tools to build the Perl-Qt wizard
AC_MSG_CHECKING([for Perl environment])
OSCAR_CHECK_PROG(PERL, perl, yes)
if test "$found_oscar_progs" = "no" ; then
  AC_MSG_WARN([*** You can not build the Perl-Qt wizard ***])
  AC_MSG_RESULT([not found])
  exit 1
fi

AC_PATH_PROG(PUIC, puic, no, [$PATH:/opt/perl-Qt/bin])
if test "$PUIC" = "no"; then
    AC_MSG_WARN([*** You will not be able to build the OSCAR wizard])
    AC_MSG_WARN([*** You need to install Perl-Qt to be able to build the wizard])
    PUIC="@echo puic"

    if test -d .svn; then 
        AC_MSG_WARN([*** Developer checkouts from Subversion *MUST* have puic])
        AC_MSG_ERROR([Cannot continue])
    fi
fi
AC_SUBST(PUIC)
AC_SUBST(PERL)

# Look for things to make the .tex documentation, but only if we have
# .tex documentation to make.

AC_MSG_CHECKING([for documentation LaTeX source])
if test -d doc/architecture -o \
    -d doc/by-laws -o \
    -d doc/development -o \
    -d doc/installation -o \
    -d doc/user; then
    AC_MSG_RESULT([found])

    OSCAR_CHECK_PROG(LATEX, latex, $enable_maintainer_mode)
    OSCAR_CHECK_PROG(PDFLATEX, pdflatex, $enable_maintainer_mode)
    OSCAR_CHECK_PROG(DVIPS, dvips, $enable_maintainer_mode)
    OSCAR_CHECK_PROG(FIG2DEV, fig2dev, $enable_maintainer_mode)
    OSCAR_CHECK_PROG(PNGTOPNM, pngtopnm, $enable_maintainer_mode)
    OSCAR_CHECK_PROG(PNMTOPS, pnmtops, $enable_maintainer_mode)
    OSCAR_CHECK_PROG(LATEX2HTML, latex2html, $enable_maintainer_mode)
    if test "$found_oscar_progs" = "no"; then
	AC_MSG_WARN([*** You will not be able to build the OSCAR documentation])
	doc_target=warning

	# Option for the HTML documentation

	LATEX2HTML_OPTIONS="--split=6 -local_icons -long_titles 3 -auto_navigation --html_version=4.0 -show_section_numbers"
    else
	doc_target=do-nothing
    fi
else
    AC_MSG_RESULT([not found])
    LATEX=
    PDFLATEX=
    DVIPS=
    FIG2DEV=
    PNGTOPNM=
    PNMTOPS=
    LATEX2HTML=
    LATEX2HTML_OPTIONS=
    doc_target=do-nothing
fi
AC_SUBST(doc_target)

# Need to put these here because automake < 1.6 doesn't recognize
# AC_ARG_VAR -- so although the variables will be AC_SUBST'ed, they
# won't be instantiated in all the Makefiles.  Grumble...

AC_SUBST(LATEX)
AC_SUBST(PDFLATEX)
AC_SUBST(DVIPS)
AC_SUBST(FIG2DEV)
AC_SUBST(PNGTOPNM)
AC_SUBST(PNMTOPS)
AC_SUBST(LATEX2HTML)
AC_SUBST(LATEX2HTML_OPTIONS)

# Install to a fixed location

AC_MSG_CHECKING([for location of OSCAR_HOME])
if test "$prefix" = "" -o "$prefix" = "NONE"; then
    OSCAR_HOME="/opt/oscar"
    prefix="$OSCAR_HOME"
else
    OSCAR_HOME="$prefix"
fi
AC_SUBST(OSCAR_HOME)
AC_MSG_RESULT([$OSCAR_HOME])

# Is this a maintainer build?
# AC_CONFIG_FILES doesn't seem to be able to handle variables; have to
# do this all longhand.  :-(

# List all the files to be made

AC_CONFIG_FILES([
    Makefile
    VERSION

    dist/Makefile
    dist/programs.sh
    dist/oscar-release.spec 

    doc/Makefile

    lib/Makefile
    lib/OSCAR/Makefile
    lib/OSCAR/OCA/Makefile
    lib/OSCAR/OCA/OS_Detect/Makefile
    lib/Qt/Makefile
    lib/Tk/Makefile
    lib/Tk/HTML/Makefile

    images/Makefile

    oscarsamples/Makefile

    scripts/Makefile
    scripts/oscar_home.sh
    scripts/oscar_home.csh

    share/Makefile
    share/prereqs/Makefile
    share/prereqs/HTML-Tree/Makefile
    share/prereqs/HTML-Tree/media/Makefile
    share/prereqs/HTML-Tree/scripts/Makefile
    share/prereqs/oda/Makefile
    share/prereqs/oda/SRPMS/Makefile
    share/prereqs/oda/distro/Makefile
    share/prereqs/oda/distro/rhel3/Makefile
    share/prereqs/oda/scripts/Makefile
    share/prereqs/packman/Makefile
    share/prereqs/packman/RPMS/Makefile
    share/prereqs/packman/SRPMS/Makefile
    share/prereqs/packman/scripts/Makefile
    share/prereqs/update-rpms/Makefile
    share/prereqs/update-rpms/RPMS/Makefile
    share/prereqs/update-rpms/SRPMS/Makefile
    share/prereqs/update-rpms/scripts/Makefile
    share/prereqs/perl-Tk/Makefile
    share/prereqs/perl-Tk/media/Makefile
    share/prereqs/perl-Tk/media/RPMS/Makefile
    share/prereqs/perl-Tk/media/RPMS/5.6/Makefile
    share/prereqs/perl-Tk/media/RPMS/5.8/Makefile
    share/prereqs/perl-Tk/media/SRPMS/Makefile
    share/prereqs/perl-Tk/scripts/Makefile
    share/prereqs/XML-Parser/Makefile
    share/prereqs/XML-Parser/media/Makefile
    share/prereqs/XML-Parser/scripts/Makefile
    share/prereqs/XML-Simple/Makefile
    share/prereqs/XML-Simple/media/Makefile
    share/prereqs/XML-Simple/scripts/Makefile
    share/prereqs/XML-Simple/RPMS/Makefile

    src/Makefile
    src/Installer/Makefile
    src/Installer/images/Makefile
    src/NodeMgmt/Makefile
    src/Opder/Makefile
    src/Selector/Makefile
    src/NodeGroupLister/Makefile

    testing/Makefile
],[
# Arf.  Must ensure that they exist before we ensure that they're
# runnable.

if test -f scripts/oscar_home.sh; then
    chmod +x scripts/oscar_home.sh
fi
if test -f scripts/oscar_home.csh; then
    chmod +x scripts/oscar_home.csh
fi])

# Include the file generated by autogen.sh which lists all the
# Makefiles that need to be created in the package/ tree.

sinclude(dist/config_files_list.m4)

# Actually generate the files

AC_OUTPUT

