#!/usr/bin/env perl 
#
# Copyright (c) 2007-2008 Oak Ridge National Laboratory
#                         Geoffroy Vallee <valleegr@ornl.gov>
#                         All rights reserved
#
# This file is part of the OSCAR software package.  For license
# information, see the COPYING file in the top level directory of the
# OSCAR source distribution.
#
# This script is a simple CLI for the management of OSCAR clusters.
#
# $Id$
#

use strict;
use warnings "all";
use Carp;
use lib "$ENV{OSCAR_HOME}/lib";
use OSCAR::msm;
use OSCAR::PartitionMgt;
use OSCAR::NodeMgt;
use OSCAR::Database;
use OSCAR::Network;
use Getopt::Long;
use Data::Dumper;
use warnings "all";

use Data::Dumper;

# Few needed variables.
my ($new_partition, @clients, $list_partitions, $list_partition_nodes, 
    $distro, $list_partition_distro, $cluster_name, $queried_node_name,
    $partition_name, $node_to_update, $node_hostname, $node_ip, $node_type,
    $node_mac, $partition_to_deploy, $queried_partition);
my $verbose = 1;

Getopt::Long::Configure("pass_through");
GetOptions(
        "display-partitions"            => \$list_partitions,
        "display-partition-info=s"      => \$queried_partition,
        "display-partition-nodes=s"     => \$list_partition_nodes,
        "display-partition-distro=s"    => \$list_partition_distro,
        "display-node-info=s"           => \$queried_node_name,
        "update-node-info=s"            => \$node_to_update,
        "add-partition=s"               => \$new_partition,
        "deploy-partition=s"            => \$partition_to_deploy,
        "partition=s"                   => \$partition_name,
        "cluster=s"                     => \$cluster_name,
        "client=s"                      => \@clients,
        "distro=s"                      => \$distro,
        "hostname=s"                    => \$node_hostname,
        "ip=s"                          => \$node_ip,
        "type=s"                        => \$node_type,
        "mac=s"                         => \$node_mac,
        "help"                          => \&help_and_exit,
        ) || help_and_die();

# We get the configuration from the OSCAR configuration file.
my $oscar_configurator = OSCAR::ConfigManager->new();
if ( ! defined ($oscar_configurator) ) {
    die "ERROR: Impossible to get the OSCAR configuration\n";
}
my $config = $oscar_configurator->get_config();
die "ERROR: ODA is based on a real database and currently this script does ".
    "not support this mode, it supports only a file based ODA database. ".
    "Please upodate your OSCAR configuration file and re-initialize OSCAR ".
    "if you want to use this script" if ($config->{db_type} ne "file"); 

# The user wants to add a new partition
if ($new_partition) {
    # In order to add a partition, a cluster has to be specified.
    if (!defined($cluster_name)) {
        die "ERROR: no cluster is specified, impossible to create a new ".
            "partition";
    }
    # If the user did not define clients at the same time, this is an error.
    if (scalar(@clients) == 0) {
        die ("ERROR: no client is associated to the partition, ".
              "impossible to create the partition.\n");
    }
    # The user also need to spoecify a cluster.
    if (!defined($distro) || $distro eq "") {
        die ("ERROR: no linux distribution is associated to the partition. ".
              "To know which distributions are supported, please use ".
              "the oscar-config command\n");
    }
    print "New partition: name=$new_partition, ".
          "distro=$distro, clients=@clients\n" if $verbose;
    OSCAR::msm::add_partition ($cluster_name, 
                               $new_partition, 
                               $distro,
                               \@clients);
    exit 0;
}

# The user wants to see the list of partitions.
if ($list_partitions) {
    my @partitions = OSCAR::PartitionMgt::get_list_partitions ("oscar");
    foreach my $p (@partitions) {
        if (defined ($p)) {
            print "$p ";
        }
    }
    print "\n";
    exit 0;
}

# The user wants to see the list of nodes within a partition.
if ($list_partition_nodes) {
# Currently we only support a single cluster so the following code is
# deactivated.
    # In order to list partition's nodes, a cluster has to be specified.
#    if (!defined($cluster_name)) {
#        die "ERROR: no cluster is specified, impossible to create a new ".
#            "partition";
#    }
    my @nodes = OSCAR::PartitionMgt::get_list_nodes_partition ("oscar",
        $list_partition_nodes);
    die "ERROR: impossible to get nodes information" if (!@nodes);
    foreach my $n (@nodes) {
        print "$n ";
    }
    print "\n";
    exit 0;
}

# The user wants to see the Linux distribution id associated to a give.
# partition.
if ($list_partition_distro) {
    my $distro = OSCAR::PartitionMgt::get_partition_distro ("oscar",
        $list_partition_distro);
    if (defined($distro)) {
        print "$distro";
        print "\n";
    }
    exit 0;
}

# The user wants to have information regarding a specific node.
if ($queried_node_name) {
    if ($queried_node_name eq "") {
        die "ERROR: Invalid node name\n";
    }
    if (!defined($partition_name)) {
        die "ERROR: no partition is specified, impossible to get node ".
            "information";
    }
    my $node_config = OSCAR::NodeMgt::get_node_config("oscar",
        $partition_name,
        $queried_node_name);
    if ( !defined($node_config) ) {
        print "The node is not yet fully defined\n";
    }
    exit 0;
}

if ($node_to_update) {
    if ($node_to_update eq "") {
        die "ERROR: Invalid node name\n";
    }
    if (!defined($partition_name)) {
        die "ERROR: no partition is specified, impossible to get node ".
            "information";
    }
    # Few asserts to be sure we have all needed information
    if (!defined($node_hostname) || $node_hostname eq "" ||
        !defined($node_ip) || $node_ip eq "" ||
        !defined($node_type) || $node_type eq "" ||
        !defined($node_mac) || $node_mac eq "") {
        die "ERROR: invalid node information, please check you provide all ".
            "needed data";
    }
    my %node_config = (
                        'hostname'  => $node_hostname,
                        'ip'        => $node_ip,
                        'mac'       => $node_mac,
                        'type'      => $node_type,
                      );
    print Dumper %node_config;
    if (OSCAR::NodeMgt::set_node_config ("oscar",
        $partition_name,
        $node_to_update,
        \%node_config)) {
        die "ERROR: Impossible to save the node configuration\n";
    }
    exit 0;
}

if ($partition_to_deploy) {
    if ($partition_to_deploy eq "") {
        die "ERROR: Invalid node name\n";
    }
    if (OSCAR::PartitionMgt::validate_partition_data ("oscar",
                                                      $partition_to_deploy)) {
        die "ERROR: Invalid partition, missing data\n";
    }
    if (OSCAR::PartitionMgt::deploy_partition ("oscar",
                                               $partition_to_deploy)) {
        die "ERROR: an error occured while deploying the partition\n";
    }
    exit 0;
}

if ($queried_partition) {
    if ($queried_partition eq "") {
        die "ERROR: Invalid partition name\n";
    }
    if (OSCAR::PartitionMgt::display_partition_info("oscar", 
        $queried_partition)) {
        die "ERROR: Impossible to display partition info ($queried_partition)";
    }
    exit 0;
}

sub help () {
    print "Help: Not yet implemented\n";
}

sub help_and_die {
    help ();
    exit 1;
}

sub help_and_exit {
    help ();
    exit 0;
}

help_and_die();

exit 1;


__END__

=head1 NAME

oscar, a command line tool for the management of OSCAR clusters.

=head1 SYNOPSIS

oscar OPTIONS

=head1 DESCRIPTION

oscar is a command line tool for the management of OSCAR cluster. It allows
users to define and deploy clusters and cluster partitions.

=head1 SYNTAX

oscar [--add-partition | --display-partitions | --display-partition-nodes | 
       --display-partition-distro | --deploy-partition | --display-node-info |
       --update-node-info]

=head1 OPTIONS

Recognized options include:

=over 8

=item --display-partitions

=item --display-partition-nodes

=item --display-partition-distro

=item --display-node-info

=item --add-partition

=item --deploy-partition

=item --cluster

=item --partition

=item --client

=item --distro

=item --help

Help information.

=back

=head1 Examples

To add a partition "partition1" to the cluster "oscar with two nodes, "node1" 
and "node2", based on debian 4 x86_64:
    oscar --add-partition partition1 --cluster oscar --client node1 --client \
    node2 --distro debian-4-x86_64

To get information about node1 from partition1:
    oscar --display-node-info node1 --partition partition1

To set node data (node2 of partition1):
    oscar --update-node-info node2 --partition partition1 \
    --mac  00:01:01:01:01:01 --ip 192.168.1.1 --type diskfull \
    --hostname node2.oscardomain

=head1 AUTHOR

Geoffroy Vallee, Oak Ridge National Laboratory <valleegr@ornl.gov>

=head1 SEE ALSO

perl(1)

=cut
