#!/usr/bin/perl

#   This program is free software; you can redistribute it and/or modify
#   it under the terms of the GNU General Public License as published by
#   the Free Software Foundation; either version 2 of the License, or
#   (at your option) any later version.
 
#   This program is distributed in the hope that it will be useful,
#   but WITHOUT ANY WARRANTY; without even the implied warranty of
#   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#   GNU General Public License for more details.
 
#   You should have received a copy of the GNU General Public License
#   along with this program; if not, write to the Free Software
#   Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA

#   This script is designed to be run after the image is built.
#   It will do all the fixups required.
#
#   Copyright (c) 2005 The Trustees of Indiana University.  
#                      All rights reserved.

# $Id$

use strict;
use lib "$ENV{OSCAR_HOME}/lib","/usr/lib/systeminstaller";
use SIS::Client;
use SIS::Image;
use SIS::DB;
use Carp;
use File::Copy;
use File::Path;
use File::Basename;
use OSCAR::Package;
use OSCAR::Database;
use AppConfig;


get_numproc();

# This makes sure existing nodes have the latest /etc/hosts. 
# The profile script is execd so we know the path (from opium's post_install)
my $cmd;
my $os = OSCAR::OCA::OS_Detect::open();
if (!defined $os) {
    die "ERROR: Impossible to detect the local OS";
}
if ($os->{pkg} eq "rpm") {
    $cmd = ". /etc/profile.d/c3.sh &&";
}
$cmd .= "cpush /etc/hosts";
system("$cmd") and carp("Couldn't push /etc/hosts to all nodes");

my $return = 0;
my @pkgs = list_selected_packages();
foreach my $pkg_ref (@pkgs) {
    my $pkg = $$pkg_ref{package};
    if(!OSCAR::Package::run_pkg_script($pkg, "post_install", 1, "")) {
        carp("Couldn't run 'post_install' script for $pkg");
        $return++;
    }
}

if($return) {
    carp("Some of the post install scripts failed, please check your logs for more info");
} else {
    print "Cluster setup complete!\n";
}

exit($return);
 
# Use Schwartzian transform to sort clients by node names alphabetically and numerically.
# Names w/o numeric suffix precede those with numeric suffix.
sub sortclients(@) {
	return map { $_->[0] }
	       sort { $a->[1] cmp $b->[1] || ($a->[2]||-1) <=> ($b->[2]||-1) }
	       map { [$_, $_->name =~ /^([\D]+)([\d]*)$/] }
	       @_;
}

sub get_numproc {
	my @machines= sortclients list_client();
	foreach my $mach (@machines) {
		my $CMD="/usr/bin/ssh -n ".$mach->hostname." cat /proc/cpuinfo";
		print "Gathering processor count from ".$mach->hostname.".\n";
		open (CPIPE,"$CMD |") or (carp("Unable to query machine ".$mach->hostname),next);
		my $count=0;
		while (<CPIPE>) {
			++$count if (/^processor/);
		}
		close(CPIPE);
		if (($count !~ /^[0-9]+$/) || ($count == 0)) {
			carp("Improper count ($count) returned from machine ".$mach->hostname);
		} else {
			print "Updating database for machine ".$mach->hostname." cpus=$count.\n";
			$mach->proccount($count);
			set_client($mach);
		}
	}
}

