#!/usr/bin/perl

#   This program is free software; you can redistribute it and/or modify
#   it under the terms of the GNU General Public License as published by
#   the Free Software Foundation; either version 2 of the License, or
#   (at your option) any later version.
 
#   This program is distributed in the hope that it will be useful,
#   but WITHOUT ANY WARRANTY; without even the implied warranty of
#   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#   GNU General Public License for more details.
 
#   You should have received a copy of the GNU General Public License
#   along with this program; if not, write to the Free Software
#   Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA

#   This script is designed to be run after the image is built.
#   It will do all the fixups required.
#
#   Copyright (c) 2005 The Trustees of Indiana University.
#                      All rights reserved.
#   Copyright (c) 2008 Oak Ridge National Laboratory
#                      Geoffroy Vallee <valleegr@ornl.gov>
#                      All rights reserved.

# $Id$

BEGIN {
    if (defined $ENV{OSCAR_HOME}) {
        unshift @INC, "$ENV{OSCAR_HOME}/lib";
    }
}

use strict;
use lib "/usr/lib/systeminstaller";
use SIS::Client;
use SIS::Image;
use SIS::NewDB;
use File::Copy;
use File::Path;
use File::Basename;
use OSCAR::Database;
use OSCAR::Logger;
use OSCAR::Package;
use Getopt::Long;
use Carp;

my ($verbose);
GetOptions(
            "verbose"   => \$verbose,
          );

my $initial_verbose_value = 0;
$initial_verbose_value = $ENV{OSCAR_VERBOSE} if (defined $ENV{OSCAR_VERBOSE});

if ($verbose) {
    $ENV{OSCAR_VERBOSE} = 5;
}

my $errors = 0;
my @failed_opkgs;

if (get_numproc()) {
    carp "ERROR: Impossible to get proc number";
    $errors++;
}

# This makes sure existing nodes have the latest /etc/hosts. 
# The profile script is execd so we know the path (from opium's post_install)
my $cmd;
# my $os = OSCAR::OCA::OS_Detect::open();
# if (!defined $os) {
#     die "ERROR: Impossible to detect the local OS";
# }
# if ($os->{pkg} eq "rpm") {
#     $cmd = ". /etc/profile.d/c3.sh &&";
# }
# $cmd .= "cpush /etc/hosts";
# if (system("$cmd") != 0) {
#     carp("ERROR: Couldn't push /etc/hosts to all nodes");
#     $errors++;
# }

my @pkgs = list_selected_packages();
foreach my $pkg (@pkgs) {
    if(!OSCAR::Package::run_pkg_script($pkg, "post_install", 1, "")) {
        carp("ERROR: Couldn't run 'post_install' script for $pkg");
        push (@failed_opkgs, $pkg);
        $errors++;
    }
}

if($errors) {
    carp("ERROR: Some of the post install scripts failed, please check your ".
         "logs for more info (". join (", ", @failed_opkgs).")");
} else {
    oscar_log_subsection ("Cluster setup complete!");
}

$ENV{OSCAR_VERBOSE} = $initial_verbose_value;

exit($errors);

# Use Schwartzian transform to sort clients by node names alphabetically and
# numerically.
# Names w/o numeric suffix precede those with numeric suffix.
sub sortclients(@) {
	return map { $_->[0] }
	       sort { $a->[1] cmp $b->[1] || ($a->[2]||-1) <=> ($b->[2]||-1) }
	       map { [$_, $_->{name} =~ /^([\D]+)([\d]*)$/] }
	       @_;
}

# Return: 0 if success, -1 else.
sub get_numproc {
	my @machines= sortclients list_client();
	foreach my $mach (@machines) {
		my $CMD="/usr/bin/ssh -n ".$mach->{name}." cat /proc/cpuinfo";
		oscar_log_subsection ("Gathering processor count from " .
                              $mach->{name}.".)");
		open (CPIPE,"$CMD |") 
            or (carp("Unable to query machine ".$mach->{name}),next);
		my $count=0;
		while (<CPIPE>) {
			++$count if (/^processor/);
		}
		close(CPIPE);
		if (($count !~ /^[0-9]+$/) || ($count == 0)) {
			carp("Improper count ($count) returned from machine " .
                 $mach->{name});
            return -1;
		} else {
			oscar_log_subsection ("Updating database for machine " .
                                 $mach->{name} . " cpus=$count.");
			$mach->{proccount} = $count;
			SIS::NewDB::set_client($mach);
		}
	}
    return 0;
}

__END__

=head1 NAME

post_install, a script that finalizes a OSCAR cluster configuration; after the
deployment of compute nodes.

=head1 SYNOPSIS

post_install [OPTIONS]

=head1 OPTIONS

Supported options are:

=over 8

=item --verbose

Display information during execution (set OSCAR_VERBOSE to 5).

=back

=cut

