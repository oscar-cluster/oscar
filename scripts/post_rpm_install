#!/usr/bin/perl

#   This program is free software; you can redistribute it and/or modify
#   it under the terms of the GNU General Public License as published by
#   the Free Software Foundation; either version 2 of the License, or
#   (at your option) any later version.
 
#   This program is distributed in the hope that it will be useful,
#   but WITHOUT ANY WARRANTY; without even the implied warranty of
#   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#   GNU General Public License for more details.
 
#   You should have received a copy of the GNU General Public License
#   along with this program; if not, write to the Free Software
#   Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA

#   Copyright 2001-2002 International Business Machines
#                       Sean Dague <japh@us.ibm.com>
#                       Michael Chase-Salerno <bratac@us.ibm.com>
#   (C)opyright 2005    Erich Focht <efocht@hpce.nec.com>
#   Copyright (c) 2005 The Trustees of Indiana University.  
#                      All rights reserved.

#   This script is designed to be run after the image is built.
#   It will do all the fixups required.

# $Id$

use strict;
use lib "$ENV{OSCAR_HOME}/lib";
use Carp;
use File::Copy;
use File::Path;
use File::Basename;
use AppConfig;
use OSCAR::Network;
use OSCAR::Package;
use OSCAR::Database;
use OSCAR::Logger;
use Cwd;

my $imagename = shift or croak("No arguments passed - please pass <image name> and <headnode interface name>");
my $serverint = shift or croak("Only one argument passed - please pass <image name> and <headnode interface name>"); 

my $config = init_si_config();

my $targetdir = $config->default_image_dir . "/$imagename";

if(!-d $targetdir) {croak("The directory $targetdir doesn't exist");}

oscar_log_subsection("post_rpm_install: Making sure /dev/null is r/w for all");
!system("chmod a+rw $targetdir/dev/null") or croak("Couldn't set $targetdir/dev/null permissions!");

oscar_log_subsection("post_rpm_install: Syncing system files");
sync_system_files($targetdir) or croak("Couldn't sync system files");

oscar_log_subsection("post_rpm_install: Syncing user files");
sync_user_files($targetdir) or croak("Couldn't sync user files");

oscar_log_subsection("post_rpm_install: Running ssh setup");
!system("$ENV{OSCAR_HOME}/scripts/ssh_install $targetdir") or croak("Couldn't generate ssh keys");

# unmount /proc from image, if needed
if (!system("grep $targetdir/proc /proc/mounts >/dev/null 2>&1")) {
    oscar_log_subsection("post_rpm_install: unmounting /proc from image $imagename");
    !system("umount $targetdir/proc") or croak("Couldn't umount /proc from image!");
}

# Here we run all the post_rpm_install scripts

my @pkgs = list_selected_packages();

my $return = 0;

oscar_log_subsection("post_rpm_install: Running OSCAR package post_rpm_install scripts");
foreach my $pkg_ref (@pkgs) {       # %$pkg_ref has the two keys ( package, version);
    my $pkg = $$pkg_ref{package};
    if(!run_pkg_script_chroot($pkg,$targetdir)) {
       carp("Couldn't run post_rpm_install for $pkg");
       $return++;
    }
    # Config script running outside chroot, for access to
    # master databases, xml files and parsing perl modules.
    # Argument passed: image directory path.
    if(!run_pkg_script($pkg,"post_rpm_nochroot",1,$targetdir)) {
       carp("Couldn't run post_rpm_nochroot for $pkg");
       $return++;
    }
}

if($return) {
    carp("There were errors running post_rpm_install scripts.  Please check your logs.");
}

exit($return);

sub link_kernel_files {
    my ($imagedir) = @_;
    my @file;
    my $dir=cwd();
    chdir "$imagedir/boot" or croak ("Can't change to image /boot directory");
    foreach my $link ("module-info","System.map","vmlinuz") {
        unless (-e $link) {
	    @file = glob("$link*smp");
	    symlink($file[0], $link) or croak("Couldn't make symlink for $link");
	}
    }
    chdir "$dir" or croak ("Can't change back to original directory");
    return 1;
}

sub sync_system_files {
    my ($imagedir) = @_;

    my @files = qw(/etc/localtime);
    foreach my $file (@files) {
      if(-e $file) {
        copy($file,$imagedir . $file) or (carp("Couldn't populate $file to $imagedir"),
                                          return undef);
      }
    }
    return 1;
}

sub sync_user_files {
    my ($imagedir) = @_;

    my @files = qw(/etc/gshadow /etc/shadow /etc/passwd /etc/group /etc/hosts);
    foreach my $file (@files) {
      if(-e $file) {
        copy($file,$imagedir . $file) or (carp("Couldn't populate $file to $imagedir"),
                                          return undef);
      }
    }
    return 1;
}

sub init_si_config {
    my $config = new AppConfig(
                               AUTOINSTALL_SCRIPT_DIR => { ARGCOUNT => 1},
                               AUTOINSTALL_BOOT_DIR => { ARGCOUNT => 1},
                               AUTOINSTALL_TARBALL_DIR => { ARGCOUNT => 1},
                               AUTOINSTALL_TORRENT_DIR => { ARGCOUNT => 1},
                               RSYNCD_CONF => { ARGCOUNT => 1},
                               CONFIG_DIR => { ARGCOUNT => 1},
                               DEFAULT_IMAGE_DIR => { ARGCOUNT => 1},
                               DEFAULT_OVERRIDE_DIR => { ARGCOUNT => 1},
                               RSYNC_STUB_DIR => { ARGCOUNT => 1},
                               TFTP_DIR => { ARGCOUNT => 1},
                               NET_BOOT_DEFAULT => { ARGCOUNT => 1}
                              );
    $config->file("/etc/systemimager/systemimager.conf");
    
    return $config;
}
