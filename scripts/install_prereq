#!/usr/bin/env perl
#
# Copyright (c) Erich Focht <efocht@hpce.nec.com>
#               All rights reserved
# Copyright (c) 2007 The Trustees of Indiana University.  
#                    All rights reserved.
# Copyright (c) 2007, Oak Ridge National Laboratory.
#                     Geoffroy R. Vallee <valleegr@ornl.gov>
#                     All rights reserved.
#
# This file is part of the OSCAR software package.  For license
# information, see the COPYING file in the top level directory of the
# OSCAR source distribution.
#
# $Id$

use strict;
use Getopt::Long;
use lib "$ENV{OSCAR_HOME}/lib";
use OSCAR::OCA::OS_Detect;
use OSCAR::PackagePath;
use OSCAR::CmpVersions;
use OSCAR::Prereqs;
use OSCAR::Logger;
use Data::Dumper;
use Carp;

use vars qw($pm $ohome $dumb $smart @prereqs $separator $oscar_pkg_pool 
            $distro_pkg_pool);

my ($verbose, $path, $status, $remove);
GetOptions(
           "dumb"      => \$dumb,
           "smart"     => \$smart,
           "remove"    => \$remove,
           "status"    => \$status,
           "verbose"   => \$verbose,
           ) || &usage(1);


# Remove a prereq. Remember that the install and remove lists are defined based
# on the installation phase. So here, we need to remove the packages that are
# installed at installation time.
#
# Return: 0 if success, value different to 0 else.
sub remove_prereq ($$$$$$) {
    my ($distro, $distrover, $arch, $prereqs, $path, $suffix) = @_;
    
    my ($installs, $removes, $shellcmds) = 
        OSCAR::Prereqs::get_rawlist_prereqs($distro, $distrover, $arch, @$prereqs);

    require OSCAR::PackMan;
    if ($suffix eq "rpm") {
        $pm = PackMan::RPM->new;
    } elsif ($suffix eq "deb") {
        $pm = PackMan::DEB->new;
    } else {
        croak "Support for dumb $suffix install is not implemented.\n";
        return -1;
    }
    OSCAR::Logger::oscar_log_subsection ("Following packages will be removed: ".
        join(", ",@$installs));
    my ($err, @out) = $pm->smart_remove(@$installs);
    return $err;
}

sub install_prereq($$$$$$) {
    my ($distro, $distrover, $arch, $prereqs, $path, $suffix) = @_;

    if ( ! -d $path) {
        carp "ERROR: Invalid path ($path)";
        return -1;
    }

    my ($installs, $removes, $shellcmds) = 
        OSCAR::Prereqs::get_prereqs($distro, $distrover, $arch, @$prereqs);

    # try to detect smart installer, fall-back to dumb installer if not found
    if (!$dumb) {
        $dumb = 1;
        $smart = 0;
        eval "require OSCAR::PackMan";
        if (!$@ and eval("PackMan->new()") ) {
            my $tp;
            eval "\$tp = PackMan->new()";
            if ($tp->is_smart()) {
                #(system("which yume >/dev/null 2>&1") && ($suffix eq "rpm")) or
                #(system("which rapt >/dev/null 2>&1") && ($suffix eq "deb"))) {
                $smart = 1;
                $dumb = 0;
                OSCAR::Logger::oscar_log_subsection("Detected OSCAR::PackMan, ".
                    "using smart mode");
            }
        }
        if ($dumb) {
            OSCAR::Logger::oscar_log_subsection("Could not detect ".
                "OSCAR::PackMan, falling back to dumb mode");
        }
    }

    # locate destination path for prereq package files and repository url
    $oscar_pkg_pool = &OSCAR::PackagePath::oscar_repo_url();
    $distro_pkg_pool = &OSCAR::PackagePath::distro_repo_url();
    $separator = &OSCAR::PackagePath::pkg_separator();

    OSCAR::Logger::oscar_log_subsection ("OSCAR pools: $oscar_pkg_pool");
    OSCAR::Logger::oscar_log_subsection ("Distro pools: $distro_pkg_pool");
    OSCAR::Logger::oscar_log_subsection ("Prereq paths: ".join(",",@prereqs));

    #
    # Is this "smart mode"? Then load PackMan and regenerate the metadata cache
    #
    if ($smart) {
        require OSCAR::PackMan;
        require OSCAR::PackageSmart;

        OSCAR::Logger::oscar_log_subsection "Adding repos $oscar_pkg_pool, ".
            "$distro_pkg_pool";
        my $local_repos = "$oscar_pkg_pool, $distro_pkg_pool";
        my @repos = split (",", $local_repos);
        OSCAR::Utils::print_array (@repos);
        $pm = PackMan->new;
        if (!defined $pm) {
            carp "ERROR: invalid Packman object\n";
            return -1;
        }
        my $ret = $pm->repo(@repos);
        if ($ret != scalar(@repos)) {
            OSCAR::Logger::oscar_log_subsection ("INFO: We added ".
                scalar(@repos)." but only $ret were saved");
        }
        if (!defined $pm) {
            carp "ERROR: invalid Packman object\n";
            return -1;
        }
    }

    OSCAR::Logger::oscar_log_subsection("Following packages will be removed: ".
        join(", ",@$removes));
    OSCAR::Logger::oscar_log_subsection("Following packages will be installed: ".
        join(", ",@$installs));
    OSCAR::Logger::oscar_log_subsection("Following shell commands will be ".
        "executed: ".join(", ",@$shellcmds));

    if ($dumb) {
        if ($suffix eq "rpm") {
            # do the removes before the installs
            handle_dumb_rpms(1, @$removes);
            handle_dumb_rpms(0, @$installs);
        } elsif ($suffix eq "deb") {
            # do the removes before the installs
            handle_dumb_debs(1, @$removes);
            handle_dumb_debs(0, @$installs);
        } else {
            croak "Support for dumb $suffix install is not implemented.\n";
        }
    } else {
        # do the removes before the installs
        handle_smart_pkgs($path,1,@$removes);
        handle_smart_pkgs($path,0,@$installs);
    }

    # check whether binary packages were really installed!!!
    # ...

    if (scalar(@$installs) && &check_installed(@$installs)) {
        carp "!!! ERROR: Some packages were NOT installed !!!\n";
        return -1;
    }

    for my $cmd (@$shellcmds) {
        $cmd =~ s/\$OSCAR_HOME/$ohome/g;
        OSCAR::Logger::oscar_log_subsection("Executing: $cmd\n");
        # We do not check the return code because we need here to deal with
        # warnings versus errors, and we do not have any policy to do so.
        system("$cmd");
    }
    return 0;
}


# check paths
&usage(1) if (!scalar(@ARGV));

$ohome = $ENV{OSCAR_HOME};

my $initial_verbose_value = 0;
$initial_verbose_value = $ENV{OSCAR_VERBOSE} if (defined $ENV{OSCAR_VERBOSE});
if ($verbose) {
    $ENV{OSCAR_VERBOSE} = 5;
}

for my $p (@ARGV) {
    if (! -d "$p" || ! -f "$p/prereq.cfg") {
        OSCAR::Logger::oscar_log_subsection "Error: $p is not a directory or ".
            "doesn't contain a prereq.cfg file!\n";
        usage(1);
    } else {
        push @prereqs, "$p";
    }
}

#
# Own distro detection
#
my $os = OSCAR::OCA::OS_Detect::open();
if (!defined $os) {
    die "ERROR: Unable to determine the local operating system";
}
my $distro    = $os->{distro};
my $distrover = $os->{distro_version};
my $arch      = $os->{arch};
my $suffix    = $os->{pkg};
my $dstring   = $os->{compat_distro}.$os->{compat_distrover}.
    "-".$os->{arch};
my $cstring   = "common-".$suffix."s";

my $march = $arch;
my $rarch = $arch;
if ($arch eq "i386") {
    $march = "i?86";  # used for shell-like globbing
    $rarch = "i.86";  # used for regular expressions
}elsif($arch eq "ppc64"){ # DIKIM added for supporting YDL-ppc64
    $march = "ppc64,ppc";
    $rarch = "ppc64|ppc";
}

if ($status) {
    OSCAR::Logger::oscar_log_subsection "Prereq status for $distro, ".
        "$distrover, $arch";
    my $ret = OSCAR::Prereqs::get_prereqs_status ($distro,
                                                  $distrover,
                                                  $arch,
                                                  @prereqs);
    exit $ret;
}


# collect all config lines and build list of packages to be installed/removed
# collect all shell commands to be executed
# ...
OSCAR::Logger::oscar_log_subsection "Searching prereqs for $distro, ".
    "$distrover, $arch in ".join (", ", @prereqs);

# now go with the processing
if ($smart || $dumb) {
    if (install_prereq ($distro, $distrover, $arch, \@prereqs, $path, $suffix)) {
        die "ERROR: Impossible to install the prereqs";
    }
} elsif ($remove) {
    if (remove_prereq ($distro, $distrover, $arch, \@prereqs, $path, $suffix)) {
        die "ERROR: Impossible to remove the prereqs";
    }
}

$ENV{OSCAR_VERBOSE} = $initial_verbose_value;

exit 0;

sub handle_smart_pkgs {
    my ($path, $remove, @pkgs) = @_;
    my ($err, @out);

    return if (!scalar(@pkgs));
    if ($remove) {
        OSCAR::Logger::oscar_log_subsection("smartly removing: ".join(",",@pkgs));
        ($err, @out) = $pm->smart_remove(@pkgs);
    } else {
        OSCAR::Logger::oscar_log_subsection("smartly installing: ".join(",",@pkgs));
        ($err, @out) = $pm->smart_install(@pkgs);
    }
    require OSCAR::PackManDefs;
    if ($err == OSCAR::PackManDefs::ERROR()) {
        carp "ERROR: $err error(s) occured for prereq $path ("
             .join("\n",@out).")";
        return -1;
    }
    return 0;
}

#
# Simple implementation for dumb rpm handling
#
sub handle_dumb_rpms {
    my ($remove, @pkgs) = @_;
    my $cmd;
    return if (!scalar(@pkgs));
    for my $pkg (@pkgs) {
        # make sure we deal with package names, not file names
        my $pkgname = $pkg; $pkgname =~ s/\.rpm$//;
        if ($remove) {
            $cmd = "rpm -q $pkgname >/dev/null 2>&1";
            OSCAR::Logger::oscar_log_subsection("Executing: $cmd\n");
            if (!system($cmd)) {
                # remove all instances of package
                $cmd = "rpm -ev --all --nodeps $pkgname";
                OSCAR::Logger::oscar_log_subsection("Executing: $cmd\n");
                system($cmd) or OSCAR::Logger::oscar_log_subsection "Warning: removal failed: $!\n";
            } else {
                OSCAR::Logger::oscar_log_subsection("Package $pkgname not installed.\n");
            }
        } else {
            # already installed?
            $cmd = "rpm -q $pkgname >/dev/null 2>&1";
            OSCAR::Logger::oscar_log_subsection("Executing: $cmd\n");
            if (system($cmd)) {
                # not installed, so do it now: 
                # - we try first to find the package locally
                # - if the package is not locally available, we try to find it
                #   on remote repositories.
                my $pkgpath = locate_pkg($pkg);
                if (defined $pkgpath && $pkgpath ne "") {
                    $cmd = "rpm -Uhv $pkgpath";
                    OSCAR::Logger::oscar_log_subsection("Executing: $cmd\n");
                } else {
                    # If we cannot find the package locally try to use yum
                    $cmd = "yum -y install $pkg";
                    OSCAR::Logger::oscar_log_subsection("Executing: $cmd\n");
                }
                croak "ERROR: install failed: $!\n" if (system($cmd));
            }
        }
    }
}

#
# Simple implementation for dumb debian package handling
# hacked by Erich Focht out of the dumb_rpms code, without much
# knowledge of dpkg
#
sub handle_dumb_debs {
    my ($remove, @pkgs) = @_;
    my $cmd;
    my $ret;
    return if (!scalar(@pkgs));
    for my $pkg (@pkgs) {
    	# make sure we deal with package names, not file names
	    my $pkgname = $pkg; $pkgname =~ s/\.deb$//;
    	if ($remove) {
	        # we test first if the package is installed
	        $cmd = "dpkg -l | grep \"ii  $pkgname \"";
    	    OSCAR::Logger::oscar_log_subsection("Executing: $cmd\n");
	        $ret = `$cmd`;
	        if (!OSCAR::Prereqs::is_deb_pkg_installed($pkgname)) {
        		# remove all instances of package
	        	$cmd = "apt-get remove $pkgname --assume-yes";
		        OSCAR::Logger::oscar_log_subsection("Executing: $cmd\n");
    		    system($cmd);
    	    }
	    } else {
    		# we try first to find the package locally
	    	my $pkgpath = locate_pkg($pkg);
		    if ($pkgpath) {
    		    # if we find a package locally we use it
	    	    OSCAR::Logger::oscar_log_subsection("Package $pkgname locally found $pkgpath\n");
		        $cmd = "dpkg -i $pkgpath";
		        OSCAR::Logger::oscar_log_subsection("Executing: $cmd\n");
    		    !system($cmd) or croak "install failed: $!\n";
	    	} else {
		        # if we do not find a package locally, we try to find one online
		        $cmd = "apt-get update && apt-get install $pkgname --assume-yes --allow-unauthenticated";
    		    OSCAR::Logger::oscar_log_subsection("Executing: $cmd\n");
	    	    system($cmd);
		    }
	    }
    }
}

#
# try to locate a package file in the repositories
#
sub locate_pkg {
    my ($pkg) = @_;
    my $found = 0;
    my @pkgs;
    my @globstr;
    my $pkgmatch;
    my $sepmatch = $separator."[0-9]";
    $pkgmatch = $pkg;
    if ($pkg =~ /\.$suffix$/) {
        $pkgmatch =~ s/\.$suffix$/*.$suffix/;
    } elsif ($pkg =~ /\.($rarch|noarch|all)$/) {
        my $parch = $1;
        $pkgmatch =~ s/\.$parch$/*.$parch.$suffix/;
    } else {
        $pkgmatch .= "*{$march,noarch,all}.$suffix";
    }
    # 
    my @lrepos;
    for my $repo ( split(",",$oscar_pkg_pool), split(",",$distro_pkg_pool) ) {
        if (($repo =~ /^\//) || ($repo =~ /^(file):/)) {
            $repo =~ s/^file://;
            push @lrepos, $repo;
        }
    }

    push @globstr, $ENV{OSCAR_HOME}."/packages/*/distro/{$cstring,$dstring}/$pkgmatch";
    push @globstr, $ENV{OSCAR_HOME}."/share/prereqs/*/distro/{$cstring,$dstring}/$pkgmatch";

    push @globstr, map { "$_/$pkgmatch" } @lrepos;

    OSCAR::Logger::oscar_log_subsection("globstring: ".join(" ",@globstr)."\n");
    my @tmp = glob(join(" ",@globstr));
    OSCAR::Logger::oscar_log_subsection("locate_pkg glob result: ".join(",",@tmp)."\n");

    # detect best version
    return best_version($pkg, @tmp);
}

sub best_version {
    my ($pkg, @candidates) = @_;
    my ($best, $bestversion);

    # Simple case, we do not have any candidates so there is no best version
    if (scalar(@candidates) == 0) {
        return undef;
    }

    for my $p (@candidates) {
	my ($name, $ver, $parch);
	if ($suffix eq "rpm") {
	    my $info = `rpm -q --qf "%{NAME} %{VERSION}-%{RELEASE} %{ARCH}" -p $p`;
	    my $err = $?;
	    if (!$err) {
		($name,$ver,$parch) = split(" ",$info);
	    } else {
		OSCAR::Logger::oscar_log_subsection(" ? rpm query returned $err\n");
	    }
	} elsif ($suffix eq "deb") {
	    local *IN;
	    open IN, "dpkg --info $p |" or croak("Could not open $p");
	    while (<IN>) {
		chomp;
		if (/^ Package: (.*)$/) {
		    $name = $1;
		} elsif (/^ Version: (.*)$/) {
		    $ver = $1;
		} elsif (/^ Architecture: (.*)$/) {
		    $parch = $1;
		}
	    }
	    close IN;
	}
	OSCAR::Logger::oscar_log_subsection(":: $p :: $name $ver $parch<\n");
	next if (!$name || !$ver);
	next if ($pkg !~ /^$name/);
    #$rarch = "ppc64|pcc" if $rarch eq "ppc64"; # DIKIM added it for YDL
	next if ($parch !~ /^($rarch|noarch|all)$/);
	if (!$best || (cmp_version_strings($ver, $bestversion) == 1)) {
	    OSCAR::Logger::oscar_log_subsection("   - $p selected\n");
	    $best = $p;
	    $bestversion = $ver;
	}
    }		
    return $best;
}

sub usage {
    my $ret = shift;
    print "Usage:\n";
    print "\t$0 --dumb|--smart|--status [--verbose] prereq_path\n\n";
    print "Install prerequisites located in prereq_path. They should have\n";
    print "the same directory structure like normal OSCAR packages but\n";
    print "need to contain the configuration file prereq.cfg\n";
    print "This file is required because prereqs are installed before\n";
    print "config.xml files can be parsed.\n";
    print "The option --status only displays the current prereqs status.\n";
    print "Typically it shows if prereqs packages are already installed or\n";
    print "removed.\n";
    print "If --status is not used, one of --smart|--dumb must be selected!\n";
    print "--dumb installs packages listed in the prereq.cfg file with the\n";
    print "base package manager and is unable to resolve dependencies. It \n";
    print "should be used for bootstrapping a smart package manager.\n";
    print "--smart installs prereqs with the help of a smart package manager\n";
    print " like yum(e).\n";
    exit $ret if ($ret);
}

__END__

=head1 NAME

install_prereq, a command line tool for OSCAR for the management of
prerequriments.

=head1 SYNOPSIS

install_prereq OPTIONS <prereq_path>

=head1 DESCRIPTION

install_prereq is a command line tool for OSCAR for the management of
prerequirements. Prereqs may be binary packages to install or remove, as well as
scripts to execute. The prereqs are mandatory for the usage of OSCAR.
Install prerequisites located in prereq_path. They should have the same
directory structure like normal OSCAR packages but need to contain the
configuration file prereq.cfg. This file is required because prereqs are 
installed before config.xml files can be parsed.
For more information about this configuration file and prereqs functionnalities,
please refer to the OSCAR documentation. For more information about OSCAR, 
please visit http://oscar.openclustergroup.org/.

=head2 Syntax

install_prereq [options] <prereq_path>

=over 1

=item prereq_path

The path of the configuration file for a given prereq.

=head2 Options

Recognized options include: --dumb|--smart|--status [--verbose]

=over 4

=item --dumb

Installs packages listed in the prereq.cfg file with the base package manager 
and is unable to resolve dependencies. It should be used for bootstrapping a 
smart package manager.

=item --smart

Installs prereqs with the help of a smart package manager like yum(e).

=item --status

Give the current status of a given prereq.

=item --verbose

Display debugging information.
