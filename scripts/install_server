#!/usr/bin/perl

# Copyright 2001-2002 International Business Machines
#                     Sean Dague <japh@us.ibm.com>
# Copyright (c) 2002 The Trustees of Indiana University.  
#                    All rights reserved.
# $Id: install_server,v 1.12 2004/03/16 00:40:37 tfleury Exp $
# 
#   This program is free software; you can redistribute it and/or modify
#   it under the terms of the GNU General Public License as published by
#   the Free Software Foundation; either version 2 of the License, or
#   (at your option) any later version.
 
#   This program is distributed in the hope that it will be useful,
#   but WITHOUT ANY WARRANTY; without even the implied warranty of
#   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#   GNU General Public License for more details.
 
#   You should have received a copy of the GNU General Public License
#   along with this program; if not, write to the Free Software
#   Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA

#   This is a script which does everything that needs to be done for
#   the server to work for oscar.  It is long, but hopefully well structured.

use strict;
use lib "$ENV{OSCAR_HOME}/lib";
use POSIX; # gives us uname function
use File::Path;
use File::Copy;
use File::Basename;
use OSCAR::Package;
use OSCAR::PackageBest;
use OSCAR::Network;
use OSCAR::Logger;
use OSCAR::Database;
use Carp;
use Data::Dumper;

sub do_it_or_die {
    my ( $command, $results_ref, $error ) = @_;
    return if dec_already_locked( $command, $results_ref, 1 );
    database_disconnect();
    die "$0: $error";
}


my $INTERFACE = shift or croak("No interface specified");

my $RPMPOOL = $OSCAR::Package::RPM_POOL;

# List of root's .rc files (MDK PATH fixup)
my @rcfiles = qw(/root/.bashrc /root/.tcshrc /root/.cshrc);

# Get a list of all non-core packages

my @packages = list_selected_packages("noncore");
oscar_log_subsection("Identified non-core packages: " . join(' ', @packages));

# Get list of RPMs from the non-core packages

my @rpms = ();
foreach my $package (@packages) {
    push @rpms, rpmlist($package,"oscar_server");
}

# Now install the rpms that we found

oscar_log_subsection("Installing server non-core RPMs " .
		     "(core RPMs already installed)");
install_packages(@rpms) 
    or croak("Couldn't install the required packages needed for OSCAR");
oscar_log_subsection("Successfully installed server non-core RPMs");

#
# NEST
#
# Unless node_config_revs already has the record of node_id and configurations_id,
# do the following step. Otherwise, skip it.
# 1. Insert core packages into config_opkgs table for oscar_server.
#    add_config_opkgs is a shortcut to insert a record of configurations_id and package_id
#    e.g) add_config_opkgs [$configurations_id] [$package_id]
#

# START LOCKING FOR NEST
my @tables = ("nodes", "configurations", "packages", "config_opkgs", "node_config_revs", "oda_shortcuts" );
my %options = ();
my @errors = ();
locking("write", \%options, \@tables, \@errors);

my $node_id = dec_already_locked("read_records nodes id name=oscar_server");
my $config_id = dec_already_locked("read_records configurations id name=oscar_server");
print "Inserting NON-CORE oscar packages into config_opkgs table :\n";
foreach my $pkg (@packages){
    print "$pkg is deleted\n";
    my $cmd = "delete_records config_opkgs config_opkgs.package_id=packages.id packages.name=$pkg config_opkgs.configurations_id=$config_id";
    do_it_or_die( "$cmd",
                  undef,
                  "cannot delete config_opkgs record for $pkg" );
} 
foreach my $opkg (@packages){
    my @packages_id = ();
    dec_already_locked("read_records packages id name=$opkg", \@packages_id);
    my $package_id = pop @packages_id;
    do_it_or_die( "add_config_opkgs $config_id $package_id",
                  undef,
                  "cannot create config_opkgs record for oscar_server" );
}


# Have installed Server RPMs & did not croak, so mark packages.<pkg>installed 
# true. If clients (image) fail they will set it to false. (best effort for now)

oscar_log_subsection("Marking installed bit in ODA for server non-core RPMS");

foreach my $pkg (@packages) {
        oscar_log_subsection("Set package: $pkg");
        my $ok_rslt = dec_already_locked("package_mark_installed $pkg");
        croak "Unable to mark packages.$pkg.installed\n" if (!$ok_rslt);
}
# UNLOCKING FOR NEST
unlock(\%options, \@errors);

oscar_log_subsection("Done marking installed bits in ODA");


# And do the post install script

oscar_log_subsection("Running post_server_install scripts " .
		     "for server non-core packages");
foreach my $package (@packages) {
    run_pkg_script($package,"post_server_install") 
	or croak("Failed running post_server_install for $package");
}
oscar_log_subsection("Successfully ran server non-core package ".
		     "post_server_install scripts");

# Additional housekeeping

oscar_log_subsection("Getting internal IP address");
my ($ip,$bcast,$netmask) = interface2ip($INTERFACE) 
    or croak("Couldn't determine internal ip address");
oscar_log_subsection("Got: [IP $ip]");
oscar_log_subsection("Got: [broadcast $bcast]");
oscar_log_subsection("Got: [netmask $netmask]");

oscar_log_subsection("Adding hosts to /etc/hosts");
update_hosts($ip) or croak("Couldn't add required host entries to /etc/hosts");

oscar_log_subsection("Updating /etc/profile");
update_profile() or croak("Couldn't add path entries to /etc/profile");

oscar_log_subsection("Updating /etc/exports");
update_exports($ip,$netmask) 
    or croak("Couldn't add filesystem entries to /etc/exports");

oscar_log_subsection("Updating rsyncd.conf");
update_rsyncd_conf($ip,$netmask) or croak("Couldn't update rsyncd.conf file");

oscar_log_subsection("Refreshing services");
!system("/etc/init.d/nfs start") or croak("Couldn't start nfs");
!system("exportfs -a") or croak("Couldn't refresh nfs exports");
!system("/etc/init.d/sshd start") or croak("Couldn't start sshd");
!system("chkconfig --level 345 sshd on");
!system("chkconfig --level 345 dhcpd on");
!system("chkconfig --level 345 portmap on");
!system("chkconfig --level 345 nfs on");

# Mandrake fixup: comment out hard-coded PATH line

oscar_log_subsection('Fixing root "dot" files');
foreach my $f (@rcfiles) {
    unmunge_pathenv($f);
}

# All done

oscar_log_subsection("Finished server_prep script");


sub verbose {
    print join " ", @_;
    print "\n";
}

sub update_hosts {
    my $ip = shift;
    if( ! $ip ) {   # mjc - 12/13/01
      croak( "Cannot update hosts without a valid ip.\n" );
    }
    verbose("Backing up /etc/hosts");
    copy("/etc/hosts","/etc/hosts.bak") or return undef;
    my $short;
    my $hostname = qx/hostname/;
    chomp($hostname);
    if($hostname =~ /\./) {
        $short = $hostname;
        $short =~ s/\..*//;
        if($short eq $hostname) {
            $short=undef;
        }
    }
    my @aliases=qw(oscar_server nfs_oscar pbs_oscar);
    open(IN,"</etc/hosts.bak") or return undef;
    open(OUT,">/etc/hosts") or return undef;
    verbose("Adding required entries to /etc/hosts");

    # mjc - 11/12/01 - start
    # - If the ip is in there, add the oscar aliases if they
    #   aren't on the list.
    # - If the ip is not in there, add the line.
    # - If you stumble across another line in the file with a different ip
    #   but the same hostname, pull it out of the file and add it back in
    #   only after we have added the line for this specified ip
    my @hostlines = ();
    my $line;
    my $found=0;

    while ($line=<IN>) {
      chomp $line; # mjc - 12/13/01
      if( $line =~ /^$ip/ ) { # mjc - 12/13/01
        # Same ip, grab all the items on the line and add the oscar 
        # aliases if they aren't already there.
        $found = 1;
        my @items = split( /\s+/, $line ); # mjc - 12/13/01
        foreach my $alias (@aliases) {
                unless (grep(/$alias/,@items)) {
                        push (@items,$alias);
                }
        }
        # print the modified line.
        print OUT join( " ", @items )."\n";
      } elsif( $line =~ /$hostname/ ) {  # mjc - 12/13/01
        # Not the same ip, but same hostname, save this line for now
        $hostlines[$#hostlines + 1] = $line;
      } else {
        # Not a line we're interested in changing
        print OUT $line."\n"; # mjc - 12/13/01
      }
    }
    # If there wasn't a line there, add it now.
    if(!$found) {
      print OUT "$ip $hostname $short ". join( " ", @aliases )."\n";
    }
    # Add back in any lines found with the same hostname but different ip
    foreach $line ( @hostlines ) {
      print OUT $line."\n"; # mjc - 12/13/01
    }
    # mjc - 11/12/01 - end

    close(OUT);
    close(IN);
}

sub update_profile {
    verbose("Backing up /etc/profile");
    copy("/etc/profile","/etc/profile.bak") or return undef;
    open(IN,"</etc/profile.bak") or return undef;
    open(OUT,">/etc/profile") or return undef;
    verbose("Adding path entries to /etc/profile");
    while(<IN>) {
        if(/^PATH=/) {
            if(!m{\b/usr/local/bin\b}) {
                $_ =~ s{(=\".*)\"}{$1:/usr/local/bin\"};
            }
            if(!m{\b/usr/local/sbin\b}) {
                $_ =~ s{(=\".*)\"}{$1:/usr/local/sbin\"};
            }
        }
        print OUT $_;
    }
    close(OUT);
    close(IN);
}

sub update_exports {
    my $ip = shift;
    my $netmask = shift;
    my $done=0;
    verbose("Backing up /etc/exports");
    copy("/etc/exports","/etc/exports.bak") or return undef;
    open(IN,"</etc/exports.bak") or return undef;
    open(OUT,">/etc/exports") or return undef;
    verbose("Checking for /home export");
    while (<IN>) {
	if (/^\/home/) {
		verbose("Found /home entry");
		$done=1;
		if (/$ip\/$netmask\(async,rw,no_root_squash\)/) {
			verbose("Existing /home entry ok, preserved");
			print OUT $_;
		} else {
			verbose("Existing /home modified");
			chomp;
			print OUT $_." $ip/$netmask(async,rw,no_root_squash)\n";
		}
	} else {
		print OUT $_;
	}
    }
    unless ($done) {
    	verbose("Adding /home export");
	print OUT "/home $ip/$netmask(async,rw,no_root_squash)\n";
    }
    close(OUT);
    close(IN);
}

sub update_rsyncd_conf {
    my ($ip, $netmask) = @_;
    my $done = 0;

    verbose("Backing up rsyncd header stub");
    my $basefile = "/etc/systemimager/rsync_stubs/10header";
    my $backup = $basefile . '~';
    copy($basefile,$backup) or return undef;

    open(IN,"<$backup") or return undef;
    open(OUT,">$basefile") or return undef;
    while(<IN>) {
	if (/hosts allow/) {
	    verbose("Found hosts allow stanza");
	    if(/hosts allow = $ip\/$netmask/) {
		$done = 1;
		verbose("Looks like we already have it!");
	    }
	}
	if(/\[scripts\]/ and !$done) {
	    verbose("Adding hosts allow to file");
	    print OUT "hosts allow = $ip/$netmask\n\n";
	}
	print OUT $_;
    }

    close(IN);
    close(OUT);
    unlink($backup);

    !system("/usr/sbin/mkrsyncd_conf") or return undef;
    verbose("Updated rsyncd.conf file");

    return 1;
}

# tjn: 9/12/02, updated 7/31/03
# Used to fix MDK brain-dead defaults for root's .rc files.
# Input: filename (e.g., '/root/.bashrc').
sub unmunge_pathenv
{
    my $file = pop @_;

    print "Making any necessary PATH fixes to ($file)\n";
    copy($file, "$file.bak") or return;

    if (!open(IN, "<$file.bak")) {
        unlink("$file.bak");
        return;
    }
    if (!open(OUT,">$file")) {
        close(IN);
        unlink("$file.bak");
        return;
    }

    while (<IN>) {
        if (! /^\s*#/ ) {	#ignore comments

            # Bashrc fixups
            # RegEx: if processing bashrc file, check for line containing 
            #  PATH and not containing $PATH, remember value found ($1)
            if ($file =~ /bashrc/ && /(.*PATH=.*)/ &&  ! /\$PATH/) {
                $_ = "#OSCAR: Modified following line to contain all PATH\n";
                $_ .= "$1:" . '$PATH' . "\n";	
            }
            # Cshrc fixups
            # RegEx: if processing cshrc files, check for set|setenv line
            #  containing PATH and not containing ${PATH}, remember value
            #  found ($2) and stuff in front of it ($1)
            elsif ($file =~ /cshrc/ && 
                   /((set|setenv){0,1}\s+PATH\s*[=]{0,1}\s*)"(.*)"/ && 
                   ! /\$\{PATH\}/i) 
            {
                $_ = "#OSCAR: Modified following line to contain all PATH\n";
                $_ .= "$1\"$3:" . '${PATH}' . "\" \n";
            }
        }
        print OUT $_;
    }

    close(OUT);
    close(IN);
}

0;
