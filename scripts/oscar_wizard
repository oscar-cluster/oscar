#!/usr/bin/perl -w
#
# Copyright (c) 2002 The Trustees of Indiana University.  
#                    All rights reserved.
# 
# This file is part of the OSCAR software package.  For license
# information, see the LICENSE file in the top level directory of the
# OSCAR source distribution.
#
# $Id: oscar_wizard,v 1.32 2002/10/28 23:14:13 mchasal Exp $
#

use strict;
use Tk;
use Tk::ROText;
use lib "$ENV{OSCAR_HOME}/lib","/usr/lib/systemconfig","/usr/lib/systeminstaller","/usr/lib/systemimager/perl";
use POSIX;
use OSCAR::Help;
use OSCAR::Network;
use OSCAR::MAC;
use OSCAR::FrontPanel;
use OSCAR::Distro;
use OSCAR::DelNode;
use OSCAR::AddNode;
use OSCAR::Logger;
use OSCAR::Selector;
use OSCAR::Configurator;
use OSCAR::Package;
use SystemInstaller::Tk::Image;
use SystemInstaller::Tk::AddClients;
use SystemInstaller::Env;
use SystemInstaller::Tk::Common;
use POSIX;
use Carp;
use vars qw($config);

if( $#ARGV != 0 ) {
    croak "usage: $0 <cluster interface>\n";
}

if(!$ENV{OSCAR_HOME}) {
    croak "The environmental variable OSCAR_HOME is not set.  Please set it to the directory that OSCAR is untarred in, or run install_cluster again.";
}

# Some globals

my $save_text;
my $interface = $ARGV[0];

# Set some things in the environment

$ENV{OSCAR_HEAD_INTERNAL_INTERFACE}=$interface;
$ENV{PATH}= $config->binpath .":" . $ENV{PATH};

# Create the main window...
my $window = MainWindow->new();
$window->title("OSCAR Installation Wizard");

# Define the main buttons

#
# The title Group
#
my $image = "$ENV{OSCAR_HOME}/images/oscar.gif";

my $GIF_t=$window->Label(-image=> $window->Photo(-file=>$image),
                         -background=>"white");
my $GIF_t2=$window->Label(-image=> $window->Photo(-file=>$image),
                          -background=>"white");
my $Title_l=$window->Label(-text=>"Welcome to the OSCAR wizard!",
			   -background=>"white");
$GIF_t->grid($Title_l,"-",-sticky=>"nsew");

# Step 1, Select packages for installation
oscar_button($window, "Step 1:", "Select OSCAR Packages To Install", 
       [\&displayPackageSelector, $window], 'select_packages');

# Step 2, Configure selected packages
oscar_button($window, "Step 2:", "Configure Selected OSCAR Packages", 
       [\&displayPackageConfigurator, $window], 'configure_packages');

# Step 3, Define OSCAR Server
oscar_button($window, "Step 3:", "Prepare OSCAR Server For Install",
	     [\&frontpanel_window, $window, {interface => $interface} ], 
	     'prep_server');

# Step 4, Build OSCAR Client Image
oscar_button($window, "Step 4:", "Build OSCAR Client Image", 
       [\&build_oscar_image, $window], 'build_image');

# Step 5, Add Clients to OSCAR Image
oscar_button($window, "Step 5:", "Define OSCAR Clients", 
       [\&build_oscar_clients, $window, $interface], 'addclients');

# Step 6, Setup Network Booting
oscar_button($window, "Step 6:", "Setup Networking", 
       [\&mac_window, $window, {interface => $interface}], 'netboot');

my $boot=$window->Label (-text => 
			 "Before continuing, network boot all of your nodes. 
Once they have completed installation, reboot them from 
the hard drive. Once all the machines and their ethernet
adaptors are up, move on to the next step.",-relief=>"groove");
$boot->grid("-","-",-sticky=>"nsew");

# Step 7, the Oscar post install script
oscar_button($window, "Step 7:", "Complete Cluster Setup", 
       [\&run_post_install, $window], 'post_install');
oscar_button($window, "Step 8:", "Test Cluster Setup", 
       [\&run_tests, $window], 'test_install');

#
# The buttons for node def maintenance
#
my $maint=$window->Label (-text=>"The following buttons are for managing 
your node definitions after the initial install.",-relief=>"groove");
$maint->grid("-","-",-sticky=>"nsew");
oscar_button($window, "", "Add OSCAR Clients", 
	     [\&addnode_window, $window, $interface], 'add_nodes');
oscar_button($window, "", "Delete OSCAR Clients", 
	     [\&delnode_window, $window], 'delete_nodes');

#
# The close button
#
my $cl_b=$window->Button(-text=>"Quit",
       -borderwidth=>"6", -relief=>"groove",
       -command=> sub {$window->destroy},-pady=>"2");
$window->bind("<Escape>",sub {$cl_b->invoke()});
$cl_b->grid("-","-",-sticky=>"nsew",-ipady=>"4");

#
# Execution goes into an infinite Tcl/Tk loop here
#

MainLoop;

###########################################################################

sub oscar_button {
    my ($window, $labeltxt, $buttontext, $buttoncmd, $helptext) = @_;
    my $label = $window->Label(-text => $labeltxt);
    my $button = $window->Button(-text => $buttontext, 
                                 -command => $buttoncmd, 
                                 -pady => 2);
    my $help = $window->Button(-text=>"Help",
                               -command=> [\&open_help, $window, $helptext], 
                               -pady => 2);
    $label->grid($button, $help, -sticky => "nsew");
}

###########################################################################

sub build_oscar_image {
    my ($window) = @_;

    my @df_lines = `df /`;
    my $disk_type = "ide";
    $disk_type = "scsi" if ( grep ( /\/dev\/sd/, ( @df_lines ) ) );

    oscar_log_section("Running step 4 of the OSCAR wizard");

    # Get the distro list

    my $arch = (uname)[4];
    $arch =~ s/i.86/i386/;
    my ($distroname, $distroversion) = which_distro('/tftpboot/rpm');
    oscar_log_subsection("Step 4: Identified distro of clients: $distroname $distroversion");
    my $rpmlist = "$ENV{OSCAR_HOME}/oscarsamples/$distroname-$distroversion-$arch.rpmlist";
    oscar_log_subsection("Step 4: Using RPM list: $rpmlist");
    
    # Get a list of the client RPMs that we want to install.  Make a
    # new file containing the names of all the RPMs to install.

    my @pkgs = list_install_pkg("all");
    my $outfile = "/tmp/oscar-install-rpmlist.$$";
    my $save_text = $outfile;
    open(OUTFILE, ">$outfile") or croak("Could not open $outfile");
    foreach my $pkg (@pkgs) {
	my @rpms = rpmlist($pkg, "client");
	foreach my $rpm (@rpms) {
	    print OUTFILE "$rpm\n";
	}
    }
    close(OUTFILE);
    my $extraflags = " --filename=$outfile";

    my $diskfile = "sample.disk.$disk_type" . 
	(($arch eq "i386") ? "" : ".$arch");

    my %vars = (
                imgname => "oscarimage",
                noshow => {
                           arch => 1,
                           imgpath => 1,
                           password => 1,
                          },
                pkgfile => "$rpmlist",
                pkgpath => "/tftpboot/rpm",
                diskfile => "$ENV{OSCAR_HOME}/oscarsamples/$diskfile",
                ipmeth => "static",
                piaction => "beep",
                extraflags => $extraflags,
                postinstall => \&postimagebuild,
               );

    createimage_window($window, %vars);
}

sub postimagebuild {
    my ($vars) = @_;

    my $cmd = "./post_rpm_install $$vars{imgname} $interface";
    oscar_log_subsection("Step 4: Running: $cmd");
    !system($cmd) or (carp($!), return undef);

    # All done -- remove the temp file that held all the RPM filenames
    
    unlink($save_text);

    oscar_log_subsection("Step 4: Successfully ran: $cmd");
    oscar_log_subsection("Step 4: Completed successfully");

    return 1;
}

###########################################################################

sub build_oscar_clients {
    my ($window, $interface) = @_;
    my ($ip, $broadcast, $netmask) = interface2ip($interface);
    my ($a, $b, $c, $d) = split(/\./,$ip);
    $d++;
    my $startip = "$a.$b.$c.$d";

    my $hostname = (uname)[1];
    my ($shorthostname,$domainname)  = split(/\./,$hostname,2);

    oscar_log_section("Running step 5 of the OSCAR wizard");

    # If the domainname is blank, stick in a default value

    if (!$domainname) {
	$domainname = "oscardomain";
    }

    my %vars = (
                imgname => "oscarimage",
                basename => "oscarnode",
                domainname => $domainname,
                numhosts => "0",
                startinghostnum => "1",
                netmask => $netmask,
                gateway => $ip,
                startip => $startip,
                postinstall => \&postaddclients,
                noshow => {
                           endip => 1,
                           endinghostnum => 1,
                          },
               );
    addclients_window($window, %vars);
}

sub postaddclients {
    my ($vars) = @_;

    my $cmd = "./post_clients";
    oscar_log_subsection("Step 5: Running: $cmd");
    !system($cmd) or (carp($!), return undef);
    oscar_log_subsection("Step 5: Successfully ran: $cmd");
    oscar_log_subsection("Step 5: Completed successfully");
    return 1;
}

###########################################################################

sub run_tests {
    my ($window) = @_;
    $window->Busy(-recurse => 1);

    oscar_log_section("Running step 8 of the OSCAR wizard");

    my $cmd = "cd ../testing && xterm -sl 500 -e ./test_cluster --wait";
    oscar_log_subsection("Step 8: Running tests: $cmd");
    !system("$cmd &") or (carp($!), $window->Unbusy(), return undef);
    oscar_log_subsection("Step 8: Not waiting for completion");
    $window->Unbusy();
    return 1;
}

###########################################################################

sub run_post_install {
    my ($window) = @_;
    $window->Busy(-recurse => 1);

    oscar_log_section("Running step 7 of the OSCAR wizard");

    my $cmd = "./post_install";
    oscar_log_subsection("Step 7: Running post_install: $cmd");
    my $rc = system($cmd);
    if($rc) {
        $window->Unbusy();
        error_window($window, "There was an error running the post install scripts, please check your logs.");
    } else {
	oscar_log_subsection("Step 7: Successfully ran post_install");
        $window->Unbusy();
        done_window($window, "Cluster Setup Complete");
    }
    oscar_log_subsection("Step 7: Completed successfully");
    return 1;
}


