#!/usr/bin/perl -w
#
#   This program is free software; you can redistribute it and/or modify
#   it under the terms of the GNU General Public License as published by
#   the Free Software Foundation; either version 2 of the License, or
#   (at your option) any later version.
#
#   This program is distributed in the hope that it will be useful,
#   but WITHOUT ANY WARRANTY; without even the implied warranty of
#   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#   GNU General Public License for more details.
#
#   You should have received a copy of the GNU General Public License
#   along with this program; if not, write to the Free Software
#   Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307 USA
#
# Copyright (c) 2003, The Board of Trustees of the University of Illinois.
#                     All rights reserved.
#
# Copyright (c) 2002-2005 The Trustees of Indiana University.  
#                    All rights reserved.
#
# Copyright (c) 2005-2006 Bernard Li <bli@bcgsc.ca>
#                         All rights reserved.
#
# Copyright (c) 2006 Erich Focht <efocht@hpce.nec.com>
#                    All rights reserved.
# 
# This file is part of the OSCAR software package.  For license
# information, see the COPYING file in the top level directory of the
# OSCAR source distribution.
#
# $Id$
#

use strict;
use Tk;
use Tk::ROText;
use lib "$ENV{OSCAR_HOME}/lib","/usr/lib/systemconfig","/usr/lib/systeminstaller","/usr/lib/systemimager/perl";
use POSIX;
use OSCAR::Help;
use OSCAR::Network;
use OSCAR::GUI_MAC;
use OSCAR::Database;
use OSCAR::Distro;
use OSCAR::DelNode;
use OSCAR::AddNode;
use OSCAR::Logger;
use OSCAR::Configurator;
use OSCAR::Package;
use OSCAR::PackagePath;
use OSCAR::Database;
use OSCAR::PackageInUn;
use OSCAR::WizardEnv;
use AppConfig;
use SystemInstaller::Tk::Image;
use SystemInstaller::Tk::AddClients;
use SystemInstaller::Env;
use SystemInstaller::Tk::Common;
use Carp;
use FileHandle;
use vars qw($config);

# First of all, enforce that the user running this script is 'root'
croak "You must be 'root' to run this script.  Aborting" if ($< != 0);

if(!$ENV{OSCAR_HOME}) {
    croak "The environmental variable OSCAR_HOME is not set.  Please set it to the directory that OSCAR is untarred in, or run oscar_wizard again.";
}

# Some globals

my $save_text;
my %options = ();
my @errors = ();
my $debug = $ENV{DEBUG_OSCAR_WIZARD};
my $mode = "manage"; # default mode is manage

our $monitortk_pid;

my $lockfile = "$ENV{OSCAR_HOME}/.oscar_wizard_lockfile";

# END processing to remove the lockfile when we die(), exit, or otherwise
# cease to exist.
END {
    unlink $lockfile if $lockfile && -f $lockfile;
}

# Check for the lockfile (this is certainly not foolproof -- it's just
# "good enough")

if (-f $lockfile) {
    open(LOCKFILE, $lockfile);
    my $pid = <LOCKFILE>;
    close(LOCKFILE);
    chomp $pid;
    print "There is an OSCAR Wizard lockfile that says a process\n";
    print "is still running with process id $pid. Checking if that is true ...\n";
    if( kill 0, $pid ) {
        print "There is a process running with that process id.\n";
        print "If this is not an OSCAR Wizard process, remove\n";
        print "the following file and run $0 again:\n";
        print "$lockfile\n";
        undef $lockfile; # Prevent END processing from deleting lockfile.
        exit(1);
    } else {
        print "There is no process running with process id $pid.\n";
        print "Removing lockfile $lockfile and continuing.\n";
        unlink $lockfile;
    }
}

# Write our PID to the lockfile

open(LOCKFILE, ">$lockfile");
print LOCKFILE $$;
close(LOCKFILE);

if ( $ARGV[0] ) {
   if ( $ARGV[0] eq "manage" ) {
      $mode = "manage";
   } elsif ( $ARGV[0] eq "install" ) {
      $mode = "install";
   } else {
      print "Usage: oscar_wizard [install|manage]\n\n";
      print "       install brings up the wizard in installation mode\n";
      print "       manage brings up the wizard in management mode (default)\n";
      exit 1;
   }
}

my $interface = get_headnode_iface();

# Set some things in the environment

$ENV{PATH}= $config->binpath .":" . $ENV{PATH};
# Create the main window...
my $window = MainWindow->new();
$window->withdraw();
chomp(my $shostname = `hostname -s`);
$window->title("OSCAR Wizard - $shostname");

# Get OSCAR's version

my $oscar_version;
my $oscar_version_script = $ENV{OSCAR_HOME} ."/" . "dist/get-oscar-version.sh";
my $oscar_version_file = $ENV{OSCAR_HOME} . "/" . "VERSION";

# Use the scripty-foo to get oscar version information.

if ( -e "$oscar_version_file" ) {
    my $cmd = "$oscar_version_script $oscar_version_file --full";

    open(CMD, "$cmd|") or die "ERROR: failed to get oscar version '$cmd' - $!";
    $oscar_version = <CMD>;
    chomp($oscar_version);

    close(CMD);
}
else {
    die("ERROR: OSCAR VERSION file missing '$oscar_version_file' - aborting!");
}

# Define the main buttons

#
# The title Group
#
my $image = "$ENV{OSCAR_HOME}/images/oscar.gif"; 

my $GIF_t=$window->Label(-image=> $window->Photo(-file=>$image),
                         -background=>"white");
my $GIF_t2=$window->Label(-image=> $window->Photo(-file=>$image),
                          -background=>"white");
my $wizard_text = "Welcome to the OSCAR Wizard!\n\nOSCAR Version: $oscar_version";
$wizard_text .= "\n\n";
if ( $mode eq "install" ) {
   $wizard_text .= "- INSTALL MODE -";
} else {
   $wizard_text .= "- MANAGE MODE -";
}
$wizard_text .= "\n\n[DEBUG ON]" if ($debug);
my $Title_l=$window->Label(-text=>"$wizard_text",
			   -background=>"white");
$GIF_t->grid($Title_l,"-",-sticky=>"nsew");

my $browser;
my $url = "http://oscar.openclustergroup.org";

# Default Web Browser is Firefox, if not found, try Mozilla
if ( !($browser = `which firefox 2> /dev/null`) ) {
  $browser = `which mozilla 2> /dev/null`;
}

# Bring up OSCAR Website when OSCAR Logo is clicked
if ($browser ne "") {
  chomp($browser);
  $GIF_t->bind( "<Button-1>" => sub { system("$browser $url > /dev/null 2>&1 &") } );
}

my $step_number;
my $step_string = "";

# Download packages using OPD
if ( $mode eq "install" ) {
   $step_number = 0;
   $step_string = "Step $step_number:"
} else {
   $step_number = undef;
   $step_string = "";
}

oscar_button($window, $step_string,
             "Download Additional OSCAR Packages...",
             [sub { my $olddir = Cwd::cwd();
		    chdir($ENV{OSCAR_HOME} . '/lib/Qt');
		    system('/usr/bin/perl Opder.pl');
		    chdir($ENV{OSCAR_HOME} . '/scripts');
		    system("./prep_oscar_repos --add --include /var/lib/oscar/packages");
		    chdir($olddir);
		}],
	     'download_packages');

if ( $mode eq "install" ) {
   # Select packages for installation
   $step_number++;
   oscar_button($window, "Step $step_number:", 
	        "Select OSCAR Packages To Install...", 
	        [sub { my $olddir = Cwd::cwd();
                 chdir($ENV{OSCAR_HOME} . '/lib/Qt');
                 system('/usr/bin/perl Selector.pl');
                 chdir($olddir);
               }], 
	        'select_packages');

   # Configure selected packages
   $step_number++;
   oscar_button($window, "Step $step_number:", 
	        "Configure Selected OSCAR Packages...", 
	        [\&displayPackageConfigurator, $window, $step_number],
	        'configure_packages');

   # Install OSCAR server packages
   $step_number++;
   oscar_button($window, "Step $step_number:", "Install OSCAR Server Packages",
	        [\&run_server_install, $window, $step_number,
	         {interface => $interface} ], 'install_server');
}

# Build OSCAR Client Image
if ( $mode eq "install" ) {
   $step_number++;
   $step_string = "Step $step_number:";
} else {
   $step_number = undef;
   $step_string = "";
}

oscar_button($window, $step_string, "Build OSCAR Client Image...", 
      [\&build_oscar_image, $window, $step_number], 'build_image');

if ( $mode eq "manage" ) {
   oscar_button($window, "", "Add OSCAR Clients...",
                [\&addnode_window, $window, $interface], 'add_nodes');
   oscar_button($window, "", "Delete OSCAR Clients...",
                [\&delnode_window, $window], 'delete_nodes');
   oscar_button($window, "",
                "Install/Uninstall OSCAR Packages...",
          [\&install_uninstall_packages, $window], 'install_uninstall_packages');
}

if ( $mode eq "install" ) {
   # Add Clients to OSCAR Image
   $step_number++;
   oscar_button($window, "Step $step_number:", "Define OSCAR Clients...", 
	        [\&build_oscar_clients, $window, $step_number, $interface], 
	        'addclients');

   # Setup Network Booting
   $step_number++;
   oscar_button($window, "Step $step_number:", "Setup Networking...", 
	        [\&mac_window, $window, $step_number,
	         {interface => $interface}], 'netboot');

   # Delete Clients returns due to popular demand!
   oscar_button($window, "", "Delete OSCAR Clients...",
                [\&delnode_window, $window], 'delete_nodes');

   # Monitor Cluster Deployment
   our $monitor_button = oscar_button($window, "", "Monitor Cluster Deployment",
                [\&monitor_deployment, $window], 'monitor_deployment');

   my $boot=$window->Label (-text => 
   			    "Before continuing, network boot all of your nodes.
   Once they have completed installation, reboot them from 
   the hard drive. Once all the machines and their ethernet
   adaptors are up, move on to the next step.",-relief=>"groove");
   $boot->grid("-","-",-sticky=>"nsew");

   # OSCAR post install script
   $step_number++;
   oscar_button($window, "Step $step_number:", "Complete Cluster Setup", 
	        [\&run_post_install, $window, $step_number], 'post_install');
}

# Test script
if ( $mode eq "install" ) {
   $step_number++;
   $step_string = "Step $step_number:"
} else {
   $step_number = undef;
   $step_string = "";
}

oscar_button($window, $step_string, "Test Cluster Setup", 
	     [\&run_tests, $window, $step_number], 'test_install');

if ( $mode eq "manage" ) {
   oscar_button($window, "", "Network Boot Manager", [\&netbootmgr, $window], 'netbootmgr') if is_installed_on_node("netbootmgr");
   oscar_button($window, "", "Ganglia Monitoring System", [\&ganglia, $window], 'ganglia') if is_installed_on_node("ganglia");
}

#
# The close button
#
my $cl_b=$window->Button(-text=>"Quit",
       -borderwidth=>"6", -relief=>"groove",
       -command=> sub {kill "HUP" => $monitortk_pid if $monitortk_pid; $window->destroy},-pady=>"2");
$window->bind("<Escape>",sub {$cl_b->invoke()});
$cl_b->grid("-","-",-sticky=>"nsew",-ipady=>"4");

if( $debug ) {
    my $cl_b=$window->Button(-text=>"Restart $0",
           -borderwidth=>"6", -relief=>"groove",
           -command=> sub {exec "$0"},-pady=>"2");
    $cl_b->grid("-","-",-sticky=>"nsew",-ipady=>"4");
}

if( $debug ) {
    my $cl_b=$window->Button(-text=>"Dump Wizard Environment",
           -borderwidth=>"6", -relief=>"groove",
           -command=> sub { use Data::Dumper; $Data::Dumper::Terse = 1; print "\nWizard Env: " . Dumper(\%ENV) . "\n\n"; }
           ,-pady=>"2");
    $cl_b->grid("-","-",-sticky=>"nsew",-ipady=>"4");
}

center_window( $window );
#
# Execution goes into an infinite Tcl/Tk loop here
#

MainLoop;

###########################################################################

sub oscar_button {
    my ($window, $labeltxt, $buttontext, $buttoncmd, $helptext) = @_;
    my $label = $window->Label(-text => $labeltxt);
    my $button = $window->Button(-text => $buttontext, 
                                 -command => $buttoncmd, 
                                 -pady => 2);
    my $help = $window->Button(-text=>"Help",
                               -command=> [\&open_help, $window, $helptext], 
                               -pady => 2);
    $label->grid($button, $help, -sticky => "nsew");

    return $button;
}

###########################################################################

sub run_command {
    my ($parent_window, $step_number, $vars) = @_;
    my $result;
    my $window_func;

    if ($mode eq "install") {
       oscar_log_subsection("Step $step_number: Running: $$vars{cmd}");
    } else {
       oscar_log_subsection("OSCAR Manage Wizard: Running: $$vars{cmd}");
    } 

    if (system($$vars{cmd})) {
	$result = $$vars{failure};
	$window_func = \&error_window;
    } else {
	$result = $$vars{success};
	$window_func = \&done_window;
    }

    if ($mode eq "install") {
       oscar_log_subsection("Step $step_number: " . $result->{message});
    } else {
       oscar_log_subsection($result->{message});
    }
    if ($result->{unbusy}) {
	&$window_func($parent_window, $result->{message},
		     sub { $parent_window->Unbusy(); });
    } else {
	&$window_func($parent_window, $result->{message});
    }
    $parent_window->Unbusy();
    $result->{return_status};
}

###########################################################################

sub run_server_install {
    my ($window, $step_number, $vars) = @_;
    my @mod_env = ();

    # Make the top-level OSCAR wizard window busy so that the user
    # can't click in another step while this one is running.

    $window->Busy(-recurse => 1);

    oscar_log_section("Running step $step_number of the OSCAR wizard: Install OSCAR server packages");

    my $cmd_vars = { 
	cmd => "./install_server $$vars{interface}",
	success => {
	    message => "Successfully installed OSCAR server",
	    unbusy => 1,
	    return_status => 0,
	},
	failure => {
	    message => "Failed to properly install OSCAR server; please check the logs",
	    unbusy => 1,
	    return_status => 1,
	},
    };
    run_command($window, $step_number, $cmd_vars);

    oscar_log_subsection('Update Wizard Env (as needed)');
    @mod_env = WizardEnv::update_env();
}

###########################################################################

sub build_oscar_image {
    my ($window, $step_number) = @_;
    my @df_lines = `df /`;
    my $disk_type = "ide";
    $disk_type = "scsi" if ( grep ( /\/dev\/sd/, ( @df_lines ) ) );

    if ($mode eq "install") {
       oscar_log_section("Running step $step_number of the OSCAR wizard: Build OSCAR client image");
    } else {
       oscar_log_section("OSCAR Manage Wizard: Build OSCAR client image");
    } 

    # Get the distro list

    my $master_os = distro_detect_or_die("/");
    my $arch = $master_os->{arch};

    my $distro = $master_os->{compat_distro};
    my $distro_ver = $master_os->{compat_distrover};

    #my @available_distros = &list_available_distros();
    #my @compat_architectures = @list_compat_archs($master_os->{arch});

    # quick hack to get this running with own (master) distro
    # adding foreign distros will need some more complicated hacking

    my $distro_pool = &OSCAR::PackagePath::distro_repo_url("/");
    $distro_pool =~ s/\ /,/g;
    my $oscar_pool = &OSCAR::PackagePath::oscar_repo_url("/");

    my $step_string = "";
    $step_string = "Step $step_number: " if $mode eq "install";

    oscar_log_subsection("$step_string"."Identified distro of clients: $distro $distro_ver");

    oscar_log_subsection("$step_string"."distro repo: $distro_pool");
    oscar_log_subsection("$step_string"."oscar repo: $oscar_pool");

    my $pkglist = "$ENV{OSCAR_HOME}/oscarsamples/$distro-$distro_ver-$arch.rpmlist";
    oscar_log_subsection("$step_string"."Using RPM list: $pkglist");
    
    # Get a list of the client RPMs that we want to install.  Make a
    # new file containing the names of all the RPMs to install.

    my @opkgs = list_selected_packages("all");
    my $outfile = "/tmp/oscar-install-rpmlist.$$";
    my @errors;
    $save_text = $outfile;
    open(OUTFILE, ">$outfile") or croak("Could not open $outfile");
    foreach my $opkg_ref (@opkgs) {
        my $opkg = $$opkg_ref{package};
        my %sel = ("group" => "oscar_clients", "os" => $master_os);
        my @pkgs = pkgs_of_opkg($opkg, undef, \@errors, %sel );
        foreach my $pkg (@pkgs) {
            print OUTFILE "$pkg\n";
        }
    }
    close(OUTFILE);
    my $extraflags = "--filename=$outfile ";
    if (exists $ENV{OSCAR_VERBOSE}) {
	$extraflags .= "--verbose ";
    }
    my $diskfile = "$ENV{OSCAR_HOME}/oscarsamples/$disk_type";
    # ia64 needs special disk file because of /boot/efi
    $diskfile .= ".$arch" if $arch eq "ia64";
    $diskfile .= ".disk";

    my %vars = (
                title => "Build OSCAR Client Image",
                imgname => "oscarimage",
                noshow => {
                           arch => 1,
                           imgpath => 1,
                           password => 1,
                          },
                pkgfile => $pkglist,
                pkgpath => "$oscar_pool,$distro_pool",
                diskfile => $diskfile,
                ipmeth => "static",
                mcast  => "off",
                piaction => "reboot",
                extraflags => $extraflags,
                postinstall => \&postimagebuild,
                step_number => $step_number,
               );

    createimage_window($window, %vars);
}

sub postimagebuild {
    my ($vars) = @_;
    my $step_number = $$vars{step_number};

    my $img = $$vars{imgname};

    my $master_os = distro_detect_or_die("/");
    my $arch = $master_os->{arch};

    # Get the image path (typically /var/lib/systemimager/images/<imagename>)
    my $config = init_si_config();
    my $imaged = $config->default_image_dir;
    croak "default_image_dir not defined\n" unless $imaged;
    croak "$imaged: not a directory\n" unless -d $imaged;
    croak "$imaged: not accessible\n" unless -x $imaged;
    my $imagepath = $imaged."/".$img;
    croak "$imagepath: not a directory\n" unless -d $imagepath;
    croak "$imagepath: not accessible\n" unless -x $imagepath;

    #
    # Image info lines should be deleted once systeminstaller
    # talks directly to ODA
    #
    my %image_info = ( "name"         => $img,
		       #
		       # EF: OS_Detect detects images now, use that!
		       #
                       # "distro"=>"$distroname-$distroversion",
                       "architecture" => $arch,
                       "path"         => $imagepath );
    set_images(\%image_info,\%options,\@errors);

    my $cmd = "./post_rpm_install $img $interface";

    my $step_string = "";
    $step_string ="Step $step_number: " if $mode eq "install";

    oscar_log_subsection("$step_string"."Running: $cmd");
    !system($cmd) or (carp($!), return undef);
    oscar_log_subsection("$step_string"."Successfully ran: $cmd");


    # Have installed Client RPMs & did not croak, so mark 
    # packages.<pkg>installed # true. (best effort for now)
    
    oscar_log_subsection("Marking installed bit in ODA for client RPMS");
    
    my @opkgs = list_selected_packages("all");
    foreach my $opkg_ref (@opkgs) {
        my $opkg = $$opkg_ref{package};
        oscar_log_subsection("Set package: $opkg");
        set_image_packages($img,$opkg,\%options,\@errors);
    }
    oscar_log_subsection("Done marking installed bits in ODA");

    # /var/log/lastlog could be HUGE in some horked setup packages...
    #
    croak "image name not defined\n" unless $$vars{imgname};
    my $lastlog = "/var/log/lastlog";
    oscar_log_subsection( "Truncating ".$img.":".$lastlog );

    my $imagelog = $imagepath.$lastlog;
    truncate $imagelog, 0 if -s $imagelog;
    oscar_log_subsection( "Truncated ".$img.":".$lastlog );

    # All done -- remove the temp file that held all the RPM filenames
    
    #unlink($save_text); # breaks createimage_window

    oscar_log_subsection("$step_string"."Completed successfully");

    1;
}

###########################################################################

sub build_oscar_clients {
    my ($window, $step_number, $interface) = @_;

    my ($ip, $broadcast, $netmask) = interface2ip($interface);
    my ($a, $b, $c, $d) = split(/\./,$ip);
    if ($d == 1) {
      $d++;
    } else {
      $d = 1;
    }
    my $startip = "$a.$b.$c.$d";

    # Select the right GW address: server-gw if one nic; interface-ip if multiple nics.

    my $gw;
    my @tables = qw( nodes nics networks );
    my @results;
    my $node = "oscar_server";
    get_nics_info_with_node($node,\@results,\%options,\@errors);
    my $ref = pop @results;
    my $nic_name = $$ref{name};
    if( @results == 1 ) {
        get_gateway($node,$interface,\@results,\%options,\@errors);
        my $gw_ref = pop @results if @results;
        $gw = $$gw_ref{gateway};
    }
    $gw ||= $ip;

    my $hostname = (uname)[1];
    my ($shorthostname)  = split(/\./,$hostname,2);
    my $dnsdomainname = `dnsdomainname`;
    chomp($dnsdomainname);

    if ($mode eq "install") {
       oscar_log_section("Running step $step_number of the OSCAR wizard: Define OSCAR clients");
    } else {
       oscar_log_section("OSCAR Management Wizard: Define OSCAR clients");
    }

    # If the domainname is blank, stick in a default value

    if (!$dnsdomainname) {
	$dnsdomainname = "oscardomain";
    }

    my %vars = (
                title => "Define OSCAR Clients",
                imgname => "oscarimage",
                basename => "oscarnode",
                domainname => $dnsdomainname,
                numhosts => "0",
                startinghostnum => "1",
                netmask => $netmask,
                gateway => $gw,
                startip => $startip,
                postinstall => \&postaddclients,
                noshow => {
                           endip => 1,
                           endinghostnum => 1,
                          },
		step_number => $step_number
               );
    addclients_window($window, %vars);
}

sub postaddclients {
    my ($vars) = @_;
    my $step_number = $$vars{step_number};

    my $cmd = "./post_clients";
    my $step_string = "";
    $step_string = "Step $step_number: " if $mode eq "install";

    oscar_log_subsection("$step_string"."Running: $cmd");
    !system($cmd) or (carp($!), return undef);
    &populate_client_group_nodes;

    oscar_log_subsection("$step_string"."Successfully ran: $cmd");
    oscar_log_subsection("$step_string"."Completed successfully");
    return 1;
}

#
# NEST
#
# This script inserts the records into node_config_revs and config_opkgs
# tables for client nodes.
# Unless node_config_revs already has the record of node_id and configurations_id,
# do the following two steps. Otherwise, skip them.
# 1. Insert node_config data for client nodes.
#    add_node_config_revs is a shortcut to insert a record of node_id, configurations_id, and version
#    e.g) add_node_config_revs [$node_id] [$configurations_id] [$version]
#         Where $version is optional (The default value of $version is 0).
# 2. Insert core packages into config_opkgs table for client nodes.
#    add_config_opkgs is a shortcut to insert a record of configurations_id and
#    package_id
#    e.g) add_config_opkgs [$configurations_id] [$package_id]
#

sub do_it_or_die {
    my ( $command, $results_ref, $error ) = @_;
    return if dec_already_locked( $command, $results_ref, 1 );
    database_disconnect();
    die "$0: $error";
}

sub populate_client_group_nodes {
    my @pkgs = list_selected_packages("all");
    my @tables = ("Nodes", "Group_Nodes", "Groups", "Packages", "Node_Package_Status");
#    locking("WRITE", \%options, \@tables, \@errors);
    my @client_nodes = ();
    get_client_nodes(\@client_nodes,\%options,\@errors);
    my @nodes = ();
    foreach my $client_ref (@client_nodes){
        my $node_id = $$client_ref{id};
        my $node_name = $$client_ref{name};
        push @nodes, $node_name;
    }
    my $client_group = "oscar_clients";
    set_group_nodes($client_group,\@nodes,\%options,\@errors);

    # We assume that all the selected packages should be installed
    my $status = 2;
    foreach my $node_name (@nodes){
        update_node_package_status(\%options,$node_name,\@pkgs,$status,\@errors);
    }     
    
#    unlock(\%options, \@errors);
}

###########################################################################

sub run_post_install {
    my ($window, $step_number) = @_;
    my @mod_env = ();

    $window->Busy(-recurse => 1);

    if ($mode eq "install") {
       oscar_log_section("Running step $step_number of the OSCAR wizard: Complete cluster setup");
    } else {
       oscar_log_section("OSCAR Management Wizard: Complete cluster setup");
    }

    oscar_log_subsection('Update Wizard Env (as needed)');
    @mod_env = WizardEnv::update_env();

    my $cmd_vars = { 
	cmd => "./post_install",
	success => {
	    message => "Successfully completed the cluster install",
	    unbusy => 1,
	    return_status => 1,
	},
	failure => {
	    message => "Failed to properly complete the cluster install; please check the logs",
	    unbusy => 1,
	    return_status => 0,
	},
    };
    run_command($window, $step_number, $cmd_vars);

    &update_client_node_package_status;
    oscar_log_subsection('Update Wizard Env (as needed)');
    @mod_env = WizardEnv::update_env();
}

sub update_client_node_package_status {    
	my @pkgs = list_selected_packages();
    my @tables = ("Nodes", "Group_Nodes", "Groups", "Packages", "Node_Package_Status");
#    locking("WRITE", \%options, \@tables, \@errors);
    my @client_nodes = ();
    get_client_nodes(\@client_nodes,\%options,\@errors);
    my @nodes = ();
    foreach my $client_ref (@client_nodes){
        my $node_id = $$client_ref{id};
        my $node_name = $$client_ref{name};
        push @nodes, $node_name;
    }

    # We assume that all the selected packages should be installed
    my $status = 8;
    foreach my $node_name (@nodes){
        update_node_package_status(\%options,$node_name,\@pkgs,$status,\@errors);
    }     
    
#    unlock(\%options, \@errors);
}

###########################################################################

sub run_tests {
    my ($window, $step_number) = @_;
    $window->Busy(-recurse => 1);

    my $title = "OSCAR Test Cluster Setup";

    if ($mode eq "install") {
       oscar_log_section("Running step $step_number of the OSCAR wizard: Test cluster setup");
    } else {
       oscar_log_section("OSCAR Management Wizard: Test cluster setup");
    }

    my $cmd = "cd $ENV{OSCAR_HOME}/testing && xterm -T '$title' -n '$title' -geometry 80x47 -sl 500 -e ./test_cluster --wait";
    my $string = "";

    if ($mode eq "install") {
        $string = "Step $step_number: Running tests";
    } else {
        $string = "Running tests";
    }

    if ($debug) {
        $string .= ": $cmd";
    }

    oscar_log_subsection("$string");
    !system("$cmd &") or (carp($!), $window->Unbusy(), return undef);
  
    if ($mode eq "install") {
       oscar_log_subsection("Step $step_number: Not waiting for completion");
    } else {
       oscar_log_subsection("Not waiting for completion");
    }
 
    $window->Unbusy();
    return 1;
}

# Pop up si_monitortk from SystemImager
sub monitor_deployment {
    my ($window) = @_;
    our $monitor_button;

    my $cmd = "/usr/sbin/si_monitortk --progress";
    $monitor_button->configure(-state => 'disabled');

    my $pipe = new FileHandle;
    our $monitortk_pid = $pipe->open("$cmd |") or die "Cannot fork: $!";
    $window->fileevent($pipe, 'readable', [\&stop_monitor_deployment, $window, $pipe]);
    return 1;
}

# Unfreeze the "Monitor Cluster Deployment" button in the main OSCAR Wizard widget
sub stop_monitor_deployment {
    my ($window, $pipe) = @_;
    our $monitortk_pid;
    our $monitor_button;

    waitpid($monitortk_pid, WNOHANG);
    $monitor_button->configure(-state => 'normal') if Tk::Exists($monitor_button);;
    $window->fileevent($pipe, 'readable', "");
}

# Pop up Network Boot Manager widget
sub netbootmgr {
    my ($window, $pipe) = @_;
    my $cmd = "/usr/sbin/netbootmgr";
    system("$cmd &");
}

# Brings up Ganglia main page using your favourite browser
sub ganglia {
    my ($window, $pipe) = @_;
    my $url = "http://localhost/ganglia";

    if ($browser ne "") {
      chomp($browser);
      system("$browser $url > /dev/null 2>&1 &");
    }
}
