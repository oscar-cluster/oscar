#!/usr/bin/perl

# Copyright © 2003, The Board of Trustees of the University of Illinois. All rights reserved.
# Copyright 2002 International Business Machines
#                Sean Dague <japh@us.ibm.com>
# Copyright (c) 2002 The Trustees of Indiana University.  
#                    All rights reserved.
#
# $Id: install_cluster,v 1.20 2003/06/27 14:36:23 brechin Exp $
# 
#   This program is free software; you can redistribute it and/or modify
#   it under the terms of the GNU General Public License as published by
#   the Free Software Foundation; either version 2 of the License, or
#   (at your option) any later version.
 
#   This program is distributed in the hope that it will be useful,
#   but WITHOUT ANY WARRANTY; without even the implied warranty of
#   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#   GNU General Public License for more details.
 
#   You should have received a copy of the GNU General Public License
#   along with this program; if not, write to the Free Software
#   Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA

#   This is the oscar installation program.  It must be called with
#   one argument which is the installation network device

use strict;
use Cwd qw(chdir cwd);

use lib cwd() . "/lib";
use vars qw($VERSION);
use OSCAR::Logger;
use OSCAR::Distro;
use POSIX;
use Carp;
use OSCAR::Database;

# First of all, enforce that the user running this script is 'root'
croak "You must be 'root' to run this script.  Aborting" if ($< != 0);

# Setting this varible makes a lot of things much easier.  It might also do
# us some good down the road

$ENV{OSCAR_HOME} = cwd();
my $lockfile = "$ENV{OSCAR_HOME}/.install_cluster_lockfile";

# Set perl to autoflush all output

$| = 1;

# Subroutine to make us die and remove the lockfile

sub oscar_die {
    my ($die_msg) = @_;

    # Kill the lockfile

    unlink($lockfile);

    # Either die or exit gracefully

    die($die_msg) if ($die_msg);
    exit(0);
}

# Check for the lockfile (this is certainly not foolproof -- it's just
# "good enough")

if (-f $lockfile) {
    open(LOCKFILE, $lockfile);
    my $pid = <LOCKFILE>;
    close(LOCKFILE);
    chmod($pid);
    print "There is an OSCAR installer lockfile that says an installer process\n";
    print "is still running with process id $pid. Checking if that is true ...\n";
    my $pids_cmd = 'ps -a | awk \'{print $1}\'';
    my @pids = `$pids_cmd`;
    chomp @pids;
    if ( grep( /^$pid$/, @pids ) ) {
	print "There is a process running with that process id.\n";
	print "If this is no an OSCAR installer process, remove\n";
	print "the following file and run $0 again:\n";
	print "$lockfile\n";
	exit(0);
    } else {
	print "There is no process running with process id $pid.\n";
	print "Removing lockfile $lockfile and continuing.\n";
	unlink $lockfile;
    }
}

# Write our PID to the lockfile

open(LOCKFILE, ">$lockfile");
print LOCKFILE $$;
close(LOCKFILE);

# Setup to capture all stdout/stderr

my $oscar_logfile = $ENV{OSCAR_HOME} . "/oscarinstall.log";
if (!open (STDOUT,"| tee $oscar_logfile") || !open(STDERR,">&STDOUT")) {
    oscar_die("ERROR: Cannot tee stdout/stderr into the OSCAR logfile:
   $oscar_logfile
Aborting the install.\n");
}

# First output banner

oscar_log_section("Running OSCAR install_cluster script");

# Fix to make multi lingualness work

$ENV{LANG} = "en_US";
$ENV{LANGUAGE} = "en_US:en";
$ENV{LC_COLLATE} = "en_US";
$ENV{LC_CTYPE} = "en_US";
$ENV{LC_MESSAGES} = "en_US";
$ENV{LC_MONETARY} = "en_US";
$ENV{LC_NUMERIC} = "en_US";
$ENV{LC_TIME} = "en_US";

# add entries to path that we know we will need

$ENV{PATH} = $ENV{PATH} .
    ':/bin:/usr/bin:/sbin:/usr/sbin:/usr/local/bin:/usr/local/sbin';

my $cmd = "cd $ENV{OSCAR_HOME}/scripts; ./wizard_prep; cd $ENV{OSCAR_HOME}";
oscar_log_subsection("Running: \"$cmd\"");
!system($cmd)
    or oscar_die("Oscar Wizard preparation script failed to complete");
oscar_log_subsection("Successfully ran wizard_prep");

my $adapter = shift;
my @results;
unless ( database_execute_command("read_records oscar.interface", \@results, 0)
&& defined(@results) ) {
  database_execute_command("add_fields oscar interface=text", undef, 1);
}

if ( !defined($adapter) || $adapter =~ /force/ ) {
    my $force = ($adapter =~ /force/);
    system("$ENV{OSCAR_HOME}/scripts/set_oscar_interface $force");
} else { 
#    print "DEBUG: Setting adapter to $adapter\n";
    database_execute_command("modify_records oscar.interface~$adapter", undef, 1);
}

database_execute_command("read_records oscar.interface", \@results, 1);
$adapter = $results[1] ? $results[1] : $results[0];
#print "DEBUG: Adapter now $adapter\n";
#print "DEBUG: results are @results\n";

$ENV{OSCAR_HEAD_INTERNAL_INTERFACE}=$adapter;

# Print some environment information

open (V, "VERSION");
my $oscar_version = "";
while (<V>) {
    chomp;
    $oscar_version = $_;
}
close(V);

oscar_log_section("Prerequisites installed");

oscar_log_subsection("OSCAR version: $oscar_version");
my $hostname = (uname)[1];
my ($shorthostname, $domainname) = split(/\./,$hostname,2);
oscar_log_subsection("Command line invocation: $0 $adapter " . 
		     join(" ", @ARGV));
oscar_log_subsection("Hostname: $shorthostname");
oscar_log_subsection("Domainname: $domainname");
oscar_log_subsection("Network interface: $adapter");
my ($distro_name, $distro_version) = which_distro_server();
oscar_log_subsection("Linux distribution: $distro_name $distro_version");
oscar_log_subsection("Kernel version: " . (uname)[2]);
oscar_log_subsection("Architecture: " . (uname)[4]);
oscar_log_subsection("Running in directory: " . cwd());
oscar_log_subsection("PATH: " . $ENV{PATH});

# Do some basic sanity checks -- fail immediately if we can tell right
# now that this install won't [eventually] succeed.

if ($shorthostname eq "localhost") {
    oscar_die("
ERROR: Your hostname is \"localhost\".
OSCAR requires that you choose another hostname.
Aborting the install.\n");
}
if ($hostname eq "localhost.localdomain") {
    oscar_die("
ERROR: Your hostname is \"localhost.localdomain\".
OSCAR requires that you choose another hostname.
Aborting the install.\n");
}
if ($distro_name eq "UnknownLinux") {
    carp("
##########################################################################
   WARNING: Installing OSCAR on an unsupported distribution of Linux!
##########################################################################\n");
}
if (!$ENV{DISPLAY}) {
    oscar_die("
ERROR: Your \"DISPLAY\" environment variable is not set, probably
indicating that you are not running in an X windows environment.
OSCAR requires that you run the installer in an X windows environment.
Aborting the install.\n");
}

# Check for 2 rpms 1 from each CD

my @filesystem=glob("/tftpboot/rpm/filesystem*rpm");
my @rsync=glob("/tftpboot/rpm/rsync*rpm");
if ((scalar(@filesystem)==0) || (scalar(@rsync)==0)) {
    oscar_die("RPMs missing in /tftpboot/rpm/ (did you copy all CDs?)");
}

# Check to see if rpms for tftp and/or tftp-server are installed.  
# If so, remove them because they conflict with the tftp-hpa rpm.
foreach my $rpm ('tftp', 'tftp-server')
  {
    open(CMD,"rpm -q $rpm |");
    my $cmd_output = <CMD>;
    close CMD;
    if ($cmd_output !~ /$rpm is not installed/)
      {
        system("rpm -e $rpm");
        oscar_log_subsection("Removing $rpm RPM");
      }
  }

# Now start the execution

system("chmod 755 packages/*/scripts/*");
chdir("scripts") or oscar_die("Couldn't chdir to scripts");

# Only setup what we have to get to the OSCAR wizard.  All the rest of
# server prep is going to be driven after the first panel of
# questions.

$cmd = "./oscar_wizard $adapter";
oscar_log_subsection("Running: \"$cmd\"");
!system("$cmd 2>&1") 
    or oscar_die("Oscar Wizard failed to run successfully");
oscar_log_subsection("Successfully ran oscar_wizard");

oscar_log_subsection("Successfully ran OSCAR install_cluster script");

# All done.  Exit gracefully, killing the lockfile.

oscar_die();
