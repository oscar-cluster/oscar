#!/usr/bin/perl
# $Id$

# Copyright (c) 2004, The Board of Trustees of the University of Illinois.
#                     All rights reserved.

#   This program is free software; you can redistribute it and/or modify
#   it under the terms of the GNU General Public License as published by
#   the Free Software Foundation; either version 2 of the License, or
#   (at your option) any later version.
 
#   This program is distributed in the hope that it will be useful,
#   but WITHOUT ANY WARRANTY; without even the implied warranty of
#   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#   GNU General Public License for more details.
 
#   You should have received a copy of the GNU General Public License
#   along with this program; if not, write to the Free Software
#   Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA

#   post_clients for torque
#                        generates torque config files from clamdr entries

use strict;
use lib "$ENV{OSCAR_HOME}/lib";
use OSCAR::Logger;
use OSCAR::LoggerDefs;
use OSCAR::Utils;
use OSCAR::Opkg;
use OSCAR::Package;
use OSCAR::SystemServices;
use OSCAR::SystemServicesDefs;
use Carp;
use Data::Dumper;
use SystemInstaller::Machine;
use OSCAR::Configurator_backend;
use Getopt::Long;  # Used to get command line option
my $default;       # Did we type --default on the command line?

my $rc=0;

#########################################################################
# Guess TORQUE home is it /usr or /opt/pbs ?
#########################################################################

my $torque_home;
# Trying to find the torque home using pbsnodes. Note that even if alternatives
# is used, pbsnodes is not part of alternatives, thus we are sure that if found,
# it's the original one (pbsnodes-torque does not exists)
if (-f '/usr/bin/pbsnodes') {
   $torque_home='/usr';
} else {
   $torque_home='/opt/pbs';
}


#########################################################################
#  compactSpaces strips off the leading and trailing spaces from a      #
#  string.  If you also pass in $compact=1, then it compresses multiple #
#  spaces within the string down to 1 space.  Also, you can pass in     #
#  $commas=1 to change commas to spaces prior to compacting.            #
#########################################################################
# ($string,$compact,$commas) -> $string
sub compactSpaces($$$)
{
  my($string,$compact,$commas) = @_;

  $string =~ s/,/ /g if ($commas);     # Change commas to spaces
  $string =~ s/^\s*//;                 # Strip off leading spaces
  $string =~ s/\s*$//;                 # Strip off trailing spaces
  $string =~ s/\s+/ /g if ($compact);  # Compact multiple spaces
  return $string; 
}

#########################################################################
#  Subroutine name : parseCommandLine                                   #
#  Parameters : none                                                    #
#  Returns    : nothing                                                 #
#  This function scans the command line for options and stores their    #
#  values in global variables.                                          #
#########################################################################
sub parseCommandLine
{
  $Getopt::Long::autoabbrev = 1;
  $Getopt::Long::getopt_compat = 1;     # Allow + for options
  $Getopt::Long::order = $PERMUTE;      # Option reordering
  &GetOptions("default" => \$default);
}

#########################################################################
#  Check to see if 'TORQUE Server' is running.  If not, then start it.  #
#########################################################################
sub startPBSserver
{
  # 1st: Start the auth daemon.
  oscar_log(5, INFO, "Starting pbs auth daemon");
  !system_service(PBS_TRQAUTHD,START)
      or (oscar_log(5, ERROR, "Couldn't start pbs trqauthd service."), return -1);

  # 2nd: Start the pbs server.
  oscar_log(5, INFO, "Starting pbs server daemon");
  !system_service(PBS_SERVER,START)
      or (oscar_log(5, ERROR, "Couldn't start pbs service on head."), return -1);

  # 3rd: Start the scheduler.

  # Make sure only one scheduler is active.
  # Config Torque sched service not to start if we use Maui
  # In order to prevent a conflict when Maui is to be used,
  # 'pbs_sched' is disabled, then.

  my @pkgs = OSCAR::Database::list_selected_packages();
  if ( 'maui' ~~ @pkgs ) {
      # Using MAUI: need to disable pbs_sched
      oscar_log(5, INFO, "Stopping pbs scheduler daemon (we're using MAUI)");
      !system_service(PBS_SCHED,STOP)
          or (oscar_log(5, ERROR, "Couldn't stop pbs scheduler service."), return -1);
  } else {
      oscar_log(5, INFO, "Starting pbs scheduler daemon (we're not using alternative scheduler)");
      !system_service(PBS_SCHED,START)
          or (oscar_log(5, ERROR, "Couldn't start pbs scheduler service."), return -1);
  }

  # NOTE: If we're using MAUI, the maui scheduler will be started by maui's api-post-deploy scripts
  # We don't need it at the moment.

  # Trying to find the qstat binary. if alternatives is used, it can be named qstat-torque
  # So we check for qstat and qstat-torque in $torque_home/bin
  my $qstat_bin="";
  if ( -f $torque_home."/bin/qstat" ) {
      my $qstat_bin=$torque_home."/bin/qstat"; }
  elsif ( -f $torque_home."/bin/qstat-torque" ) {
      my $qstat_bin=$torque_home."/bin/qstat-torque"; }
  else {
      oscar_log(5, WARNING, "Unable to find qstat binary in $torque_home/bin/");
      return -1;
  }
  # need to wait until pbs_server is up.(we are here before it is up)
  for(my $i=0; $i < 10; $i++) {
      oscar_system($qstat_bin." 2> /dev/null");
      if($? != 0) {
          oscar_log(5, INFO, "Server not ready for communication: sleeping 1 second....");
          sleep 1;
      } else {
          return 0;
      }
  }
  # 10 qstat without success, there is a problem.
  oscar_log(5, INFO, "Unable to communicatate with pbs_server");
  return -1;
}

######################################################################
#  Check to see if workq is defined                                  #
######################################################################
sub isWorkqDefined
{
  my $msg = "Checking if workq is defined... : ";
  open(CMD,$torque_home."/bin/qmgr -c 'l q workq' |");
  my $result = <CMD>;
  close(CMD);
  if($result =~ /^Queue workq/) {
    oscar_log(5, INFO, $msg."yes.");
    return 1;
  } else {
    oscar_log(5, INFO, $msg."no.");
    return 0;
  }
}

# Use Schwartzian transform to sort node names alphabetically and numerically.
# Names w/o numeric suffix preceed those with numeric suffix.
sub sortnodes(@) {
	return map { $_->[0] }
	       sort { $a->[1] cmp $b->[1] || ($a->[2]||-1) <=> ($b->[2]||-1) }
	       map { [$_, /^([\D]+)([\d]*)$/] }
	       @_;
}

########################
#  BEGIN MAIN PROGRAM  #
########################

# Read in configurator values
my $configvalues = getConfigurationValues('torque');
my $compute_on_head = ($configvalues->{compute_on_head}[0]);
my $loglevel = ($configvalues->{loglevel}[0]);
# In case configurator wasn't run, provide a default
if ($loglevel eq "") {
  $loglevel = 64 ;
}

#print "Compute on head node is set to $compute_on_head\n";

# Call the update TORQUE mom config script
#print `../packages/torque/scripts/update_mom_config`;
my $torque_scripts_dir = OSCAR::Package::get_scripts_dir("torque", 'api-post-deploy');
!oscar_system("$torque_scripts_dir/update_mom_config")
    or ($rc++, oscar_log(5, ERROR, "There were problems running $torque_scripts_dir/update_mom_config"));

if (startPBSserver()) {
    $rc++;
}

parseCommandLine();

# Update the server's nodes file (optionally for only one image).
my $image = shift; # Optionally provide an image name.

oscar_log(5, INFO, "Updating pbs_server nodes");

# Read in the current nodes as shown by 'pbsnodes -a'
open (CMD,$torque_home.'/bin/pbsnodes -a |');
my $inp = "";
my $server = "";
my $opt = "";
my $val = "";
my %pbsnodes;
while ($inp = <CMD>)
  {
    chomp($inp);
    if ($inp =~ /^\s*([^=]+)\s*$/) 
      { 
        $server = $1; 
      }
    elsif ($inp =~ /^\s*([^=]+)\s*=\s*([^=]+)\s*$/) 
      { 
        $opt = $1; 
        $val = $2; 
        $pbsnodes{$server}{$opt} = $val;
      } 
  }
close (CMD);
#print Dumper(\%pbsnodes);

# Get listing of nodes from the SIS database
my %nodes = get_machine_listing($image);

# OL: TODO: Code duplication. Need to use nodes code
if ($compute_on_head eq "YES") {
  # Get server proc count
  my $server_procs = 0;
  open(CPUINFO, "/proc/cpuinfo");
    my @lines = <CPUINFO>;
  close(CPUINFO);
  foreach my $line ( @lines ) {
    chomp($line);
    if ($line =~ /^processor\s*:.*$/){
       $server_procs++
    }
  }

  # Add server to node list hash
  my %added_node ;
  $added_node{HOST}=$ENV{HOSTNAME};
  $added_node{IPADDR}="";
  $added_node{NUM_PROCS}=$server_procs;
  $added_node{NUM_GPUS}=0; # Ignore GPUS on head (too much dangerous)
  $added_node{DOMAIN}="";
  $nodes{$ENV{HOSTNAME}}=\%added_node;
}
my $TOT_NODES = 0;
my $TOT_NP = 0;
my $hostname = "";
foreach my $node (sortnodes( keys %nodes )) 
  {
    # If NUM_PROCS is not defined for this node, skip this node entirely
    next unless ($nodes{$node}{NUM_PROCS});

    # Check each node against the output from 'pbsnodes -a'
    $hostname = $nodes{$node}{HOST};
    if ($pbsnodes{$hostname})
      {
        system($torque_home."/bin/pbsnodes -o $hostname");
        # Reset the number of processors for this node
        my $qmgr_cmd=$torque_home.'/bin/qmgr -a -e -c "set node ' . $hostname .
                     ' np = ' . $nodes{$node}{NUM_PROCS};
        # and number of GPUs if any
        if ($nodes{$node}{NUM_GPUS}>0) {
	    $qmgr_cmd.=', gpus = ' . $nodes{$node}{NUM_GPUS};
        }
        $qmgr_cmd.='"';
	system($qmgr_cmd);
        # Search the properties for 'all' and add it if not found
        my(@props) = split(/,/,$pbsnodes{$hostname}{'properties '});
        my($alldefined) = "";
        foreach my $prop (@props)
          {
            $alldefined = 1 if $prop eq 'all';
          }
        system($torque_home.'/bin/qmgr -a -e -c "set node ' . $hostname . 
               ' properties += all"') unless $alldefined;
        system($torque_home."/bin/pbsnodes -c $hostname");
        # Remove from pbsnodes hash
        delete $pbsnodes{$nodes{$node}{HOST}};
      }
    else
      { # Didn't find a match -> ADD the node
        my $qmgr_cmd=$torque_home.'/bin/qmgr -a -e -c "create node ' . $hostname .
                     ' np = ' . $nodes{$node}{NUM_PROCS};
        if ($nodes{$node}{NUM_GPUS}>0) {
            $qmgr_cmd.=' , gpus = ' . $nodes{$node}{NUM_GPUS};
        }
        $qmgr_cmd.=' , properties = all"';
        system($qmgr_cmd);
      }
    
    # Count up the number of nodes and processors
    $TOT_NODES++;
    $TOT_NP += $nodes{$node}{NUM_PROCS};
  }
# print Dumper(\%nodes);

# Now go through the remaining pbsnodes hash and delete these nodes
foreach my $node (sortnodes( keys %pbsnodes )) 
  {
    oscar_system($torque_home.'/bin/qmgr -a -e -c "delete node ' . $node . '"');
  }

if(isWorkqDefined()) {
  oscar_log(1, INFO, "workq already defined...Skipping queue creation...");
} else {
  # Next, use qmgr command to set up the values for workq
  oscar_log(1, INFO, "Creating TORQUE workq queue...");
 
  # These are default values set only when not present or when --default
  my @default_params = (
    'create queue workq',
    'set queue workq queue_type                 = Execution',
    'set queue workq resources_max.cput         = 10000:00:00',
    'set queue workq resources_max.walltime     = 10000:00:00',
    'set queue workq resources_min.cput         = 00:00:01',
    'set queue workq resources_min.ncpus        = 1',
    'set queue workq resources_min.nodect       = 1',
    'set queue workq resources_min.walltime     = 00:00:01',
    'set queue workq resources_default.cput     = 10000:00:00',
    'set queue workq resources_default.ncpus    = 1',
    'set queue workq resources_default.nodect   = 1',
    'set queue workq resources_default.walltime = 10000:00:00',
    'set queue workq enabled                    = True',
    'set queue workq started                    = True',
    'set server scheduling                      = True',
    'set server default_queue                   = workq',
    'set server mail_from                       = adm',
    'set server query_other_jobs                = True',
  );

  # These are variable parameters that are set everytime
  my @variable_params = (
    "set queue workq resources_max.ncpus        = $TOT_NP",
    "set queue workq resources_max.nodect       = $TOT_NODES",
    "set queue workq resources_available.nodect = $TOT_NODES",
    "set server resources_available.ncpus       = $TOT_NP",
    "set server resources_available.nodect      = $TOT_NODES",
    "set server resources_available.nodes       = $TOT_NODES",
    "set server resources_max.ncpus             = $TOT_NP",
    "set server resources_max.nodes             = $TOT_NODES",
    "set server scheduler_iteration             = 60",
    "set server log_events                      = $loglevel",
  );

  # First, read in the current values for the localhost
  undef $/;   # Suck in the entire files into one big string
  oscar_log(8, ACTION, "About to run: $torque_home/bin/qmgr -a -e -c \"print server \@localhost\"");
  open (CMD,$torque_home.'/bin/qmgr -a -e -c "print server @localhost" |');
  my $cmd_output = <CMD>;
  close (CMD);

  # Next, build up a command string to pass back to the qmgr command.
  my $line = "";
  my $key = "";
  my $value = "";
  my $cmd_input = "";

  # Search through the default_params for anything missing.
  foreach $line (@default_params)
    {
      ($key,$value) = split /=/, $line;
      $key = compactSpaces($key,undef,undef);
      $value = compactSpaces($value,undef,undef);
      if ($value eq "")
        { # Search for just the $key since there's no value for it.
          $cmd_input .= "$key\n" if
            ($cmd_output !~ /$key/m) || 
            (defined($default) && $key !~ /create/);
        }
      else
        { # Search for "$key =".  Output key and value if needed.
          $cmd_input .= "$key = $value\n" if
            ($cmd_output !~ /$key\s*=/m) || defined($default);
        }
    }

  # Then, force the setting of the of the variable_params.
  foreach $line (@variable_params)
    {
      ($key,$value) = split /=/, $line;
      $key = compactSpaces($key,undef,undef);
      $value = compactSpaces($value,undef,undef);
      $cmd_input .= "$key";
      $cmd_input .= " = $value"
        if ($value ne "");
      $cmd_input .= "\n";
    }

  # Run the qmgr command if necessary
  if ($cmd_input ne "")
    {
      my $pbs_server_logs = "/var/log/torque/server_logs";
      open (CMD,"|".$torque_home."/bin/qmgr -a -e ");
      print CMD $cmd_input;
      close (CMD) 
          or ($rc++, oscar_log(5, ERROR,"Failed to Configure TORQUE queues, ".
                   "check the logs at $pbs_server_logs"));
    }
}

# Set server's mom to configured state
if ($compute_on_head eq "YES") {
  oscar_log(5, INFO, "Compute on head enabled: enabling pbs_mom on head");
  !enable_system_services( (PBS_MOM) )
      or ($rc++, oscar_log(5, ERROR, "Couldn't enable pbs_mom on head (compute on head enabled)."));
  !system_service(PBS_MOM,START)
      or ($rc++, oscar_log(5, ERROR, "Couldn't start pbs_mom service on head."));
} else {
  oscar_log(5, INFO, "Compute on head disabled: disabling pbs_mom on head");
  !disable_system_services( (PBS_MOM) )
      or ($rc++, oscar_log(5, INFO, "Couldn't disable pbs_mom on head (compute on head disabled)."));
  !system_service(PBS_MOM,STOP)
      or ($rc++, oscar_log(5, ERROR, "Couldn't stop pbs_mom service on head."));
}

if ($rc > 0) {
    oscar_log(1, ERROR, "Failed to properly setup torque on OSCAR head");
    exit 1;
} else {
    oscar_log(1, INFO, "Successfully configured torque on OSCAR head");
    exit 0;
}
