#!/usr/bin/perl

# post_server_install script for netbootmgr
#
# - turn on service systemimager-server-netbootmond
# - make it switch to local boot mode after installation

BEGIN {
    if (defined $ENV{OSCAR_HOME}) {
        unshift @INC, "$ENV{OSCAR_HOME}/lib";
    }
}

use Carp;
use strict;
use OSCAR::OCA::OS_Detect;
use OSCAR::Opkg;

my $os = OSCAR::OCA::OS_Detect::open();
my $binary_format = $os->{'pkg'};

my $service = "/etc/init.d/systemimager-server-netbootmond";
if (! -f $service) {
    # GV: not sure if we should generate an error or not here.
    # I guess it depends if the netbootmgr binary package has a dependency to
    # systemimager (for systemimager-server-netbootmond).
    OSCAR::Opkg::opkg_print ("[WARN] $service is not available");
    exit (0);
}

# chkconfig is a RPM specific command, so we do not use it on Debian-like
# systems. Moreover, services are automatically added into rc2.d on Debian
if ($binary_format ne "deb") {
    !system("chkconfig --add systemimager-server-netbootmond") or
	    croak("ERROR: Could not add service systemimager-server-netbootmond!");

    !system("chkconfig systemimager-server-netbootmond on") or
	    croak("ERROR: Could not enable systemimager-server-netbootmond service!");
}

my $config_file = "/etc/systemimager/systemimager.conf";
open IN, "$config_file" or
	croak("ERROR: Could not open $config_file for reading!");
my @sic = <IN>;
close IN;

print "Setting systemimager-server-netboodmond to NET_BOOT_DEFAULT=local\n";
open OUT, "> $config_file" or
	croak("ERROR: Could not open $config_file for writing!");
for my $line (@sic) {
    if ($line =~ m/^\s*NET_BOOT_DEFAULT\s*=/) {
        print OUT "NET_BOOT_DEFAULT = local\n";
    } else {
        print OUT $line;
    }
}
close OUT;

# GV:
# - We do not do restart since we just want to be sure the service restarts 
#   without generating errors. 
# - We do not catch error for stopping (the service may already be stopped and
#   this is just fine.
# - We catch potential errors when starting the service (we want to make sure
#   the service is on)
# Other nasty details: the /etc/init.d/systemimager-server-netbootmond service
# actually rely on /usr/sbin/si_netbootmond which is a very ugly beast: if the
# /etc/systemimager/systemimager-server-rsyncd daemon is not running, 
# everything will fail without a single line of output because the 
# /var/log/systemimager/rsyncd file does not exists. So before to start the
# daemon, we check the rsyncd deamon is running.

my $mandatory_log = "/var/log/systemimager/rsyncd";
if (! -f $mandatory_log) {
    # If the log file does not exist, we check whether 
    # /etc/init.d/systemimager-server-rsyncd is running or not (this daemon actually 
    # creates the log file).
    my $parent_service = "/etc/init.d/systemimager-server-rsyncd";
    if (system ("$parent_service status")) {
        print ("[WARNING] The $mandatory_log file does not exist and the ".
               "$parent_service daemon is not running, so we do not start ".
               "monitoring deamon (we assume you are not using rsync to ".
               "deploy the compute nodes\n");
        exit 0;
    } else {
        die "ERROR: $parent_service is running but $mandatory_log does not ".
            "exist. The system is not coherent.";
    }
}

my $cmd = "$service stop";
system ($cmd);
$cmd = "$service start";
!system("$cmd") or
	croak("ERROR: Impossible to exexcute $cmd");

exit 0;

